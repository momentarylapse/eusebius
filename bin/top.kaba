use lib.std
use lib.std.sys

enum TaskStatus
	UNUSED
	SLEEPING
	RUNNING
	WAITING
	ZOMBIE
	INTERRUPT
	#DEAD

func fill(out s: string, n: int)
	while len(s) < n
		s.add(' ')

func i2s2(i: int, n: int) -> string
	let s2 = str(i)
	var s1: string
	fill(s1, n - len(s2))
	return s1 + s2

func sec2s(_sec: int) -> string
	let hour = _sec / 3600
	let min = (_sec / 60) % 60
	let sec = _sec % 60
	var s = "  :  "
	s[0] = '0' + (min / 10)
	s[1] = '0' + (min % 10)
	s[3] = '0' + (sec / 10)
	s[4] = '0' + (sec % 10)
	if hour > 0
		return "{{hour|3}}:{{s}}"
	return "   " + s

func task_mode(t: TaskInfo) -> string
	if t.mode == 0
		return "K"
	else
		return "U"

func task_status(t: TaskInfo) -> string
	let s = TaskStatus.from_int(t.status)
	if s == TaskStatus.RUNNING
		return "R"
	if s == TaskStatus.SLEEPING
		return "S"
	if s == TaskStatus.WAITING
		return "W"
	if s == TaskStatus.ZOMBIE
		return "Z"
	if s == TaskStatus.INTERRUPT
		return "I"
	return str(t.status)

func show_task(t: TaskInfo, show_parent: bool)
	var s = "{{t.pid|3}}   "
	var d = 0
	if show_parent
		s += "{{t.ppid|4}}   "
		d = 5
	s += "{{cs2s(t.name)|-22}} {{task_mode(t)}} {{task_status(t)}}    {{t.cpu/10|3}}% {{mem(t.mem*4096)|9}}  {{sec2s(t.life.sec)}}"
	print(s)
	print("\n")

func mem(size: int) -> string
	if size < 10000
		return "{{size}}"
	if size < 10000000
		return "{{size>>10}}k"
	return "{{size>>20}}m"

func main()
	init_lib()
	lib_print_endl = false
	var arg: string[]
#	get_args(arg)
	
	var allow_irq = false
	var show_parent = false
	for a in arg
		if a == "-a" or a == "--all"
			allow_irq = true
		if a == "-p" or a == "--parent"
			show_parent = true
	
	var s: SysInfo
	if !sysinfo(s)
		raise(new Exception("sysinfo error"))
	print("uptime: {{sec2s(s.uptime)}}\n")
	print("memory:   {{mem(s.totalram * s.mem_unit)|8}} total  {{mem(s.freeram * s.mem_unit)|8}} free   ")
	print("{{mem((s.totalram - s.freeram) * s.mem_unit)|8}} used  {{mem(s.sharedram * s.mem_unit)|8}} shared\n")
	print("heap:     {{mem(s.totalheap * s.mem_unit)|8}} total  {{mem(s.freeheap * s.mem_unit)|8}} free   ")
	print("{{mem((s.totalheap - s.freeheap) * s.mem_unit)|8}} used\n")
	
	var tasks: TaskInfo[]
	var tt: TaskInfo
	for pid in 0:256
		if taskinfo(pid, tt)
			if tt.status != int(TaskStatus.INTERRUPT) or allow_irq
				tasks.add(tt)
	
	print(" Pid  ")
	if show_parent
		print(" PPid")
	print(" Name                 Status   CPU      Mem       Time\n")
	for t in tasks
		show_task(t, show_parent)
	
	exit(0)
