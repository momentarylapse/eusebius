use lib.std
use lib.std.file
use os


func hd(h: int, _skip: int, _length: int)
	var skip = _skip
	var length = _length
	var s: string
	while skip > 0
		s.resize(128)
		let n = min(128, skip)
		let r = read(h, &s[0], n)
		if r < 0
			print("\nerror reading...\n")
		if r <= 0
			return
		skip -= r
	while true
		s.resize(128)
		let n = min(128, length)
		let r = read(h, &s[0], n)
		if r < 0
			print("\nerror reading...\n")
		if r <= 0
			break
		s.resize(r)
		print(s.hex())
		length -= r
	print("\n")


func main()
	init_lib()
	lib_print_endl = false
	var arg: string[]
	get_args(arg)
	
	var files: Path[]
	
	var length = 2000000000
	var skip = 0
	
	for i in 1:len(arg)
		if arg[i] == "-n"
			i ++
			if i < len(arg)
				length = int(arg[i])
		else if arg[i] == "-s"
			i ++
			if i < len(arg)
				skip = int(arg[i])
		else
			files.add(arg[i])
	
	if files.num == 0
		hd(stdin, skip, length)
	
	for f in files
		let h = open(f, O_RDONLY)
		if h < 0
			print("hd: can not open file: {{f}}\n")
			exit(-1)
		hd(h, skip, length)
		close(h)
	
	exit(0)
