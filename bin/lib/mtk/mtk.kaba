use "../std.kaba"
use "../x.kaba"
use "../draw.kaba"
use "../ttfx.kaba"

const int FRAME_BAR = 30
const int FRAME_WIDTH = 10
const int GRID_MARGIN = 10

color col_frame
color col_text
color col_background
color col_title
color col_button
color col_button_frame
color col_button_hover
color col_button_hover_frame
color col_button_click
color col_button_click_frame

class MtkWidget
	MtkWindow* win
	XPainter* p
	XRect r // relative to window
	int mx, my
	/*int offset_x, offset_y
	int width, height*/
	
	void __init__()
		win = nil
		p = nil
		mx = 0
		my = 0
	
	virtual void __delete__()
		if p
			delete p
	
	void set_win(MtkWindow* win, int x, int y, int w, int h)
		self.win = win
		win.widgets.add(self)
		p = new XPainter(win.p, x, y, w, h)
		r.x0 = x
		r.y0 = y
		r.x1 = x + w
		r.y1 = y + h
		//width = w
		//height = h
	
	virtual void draw()
	virtual void onMouseMove()
	virtual void onMouseLeftButtonDown()
	virtual void onMouseLeftButtonUp()
	virtual void onKey(int key)

class MtkLabel extends MtkWidget
	string text
	void __init__(string text)
		self.text = text
	void set_text(string text)
		if self.text == text
			return
		self.text = text
		if p
			draw()
			p.end()
	override void draw()
		p.set_color(col_background)
		p.rectangle(p.all)
		p.fill()
		p.set_color(col_text)
		p.draw_str(5, 5, text, 0.007)

class MtkGrid extends MtkWidget
	int nx, ny
	MtkWidget*[] children
	void __init__(int _nx, int _ny)
		nx = _nx
		ny = _ny
		children.resize(nx * ny)
		for c in children
			c = nil
	override void draw()
		p.set_color(col_background)
		p.rectangle(p.all)
		p.fill()
		for c in children
			if c
				c.draw()
	void put(int x, int y, MtkWidget *c)
		children[x + y * nx] = c
		int ww = (r.w() - (nx - 1) * GRID_MARGIN) / nx
		int hh = (r.h() - (ny - 1) * GRID_MARGIN) / ny
		int dx = (ww + GRID_MARGIN) * x
		int dy = (hh + GRID_MARGIN) * y
		c.set_win(win, r.x0 + dx, r.y0 + dy, ww, hh)

enum
	BUTTON_STATE_DEFAULT
	BUTTON_STATE_HOVER
	BUTTON_STATE_CLICK

class MtkButton extends MtkWidget
	string text
	int state
	void __init__(string text)
		self.text = text
		state = BUTTON_STATE_DEFAULT
	void set_text(string text)
		if self.text == text
			return
		self.text = text
		if p
			draw()
			p.end()
	override void draw()
		p.set_color(col_button)
		if state == BUTTON_STATE_HOVER
			p.set_color(col_button_hover)
		if state == BUTTON_STATE_CLICK
			p.set_color(col_button_click)
		p.rectangle(p.all)
		p.fill()
		
		p.set_color(col_button_frame)
		if state == BUTTON_STATE_HOVER
			p.set_color(col_button_hover_frame)
		if state == BUTTON_STATE_CLICK
			p.set_color(col_button_click_frame)
		p.line_width = 4
		p.rectangle(rect(3, r.w()-3, 3, r.h()-3))
		p.strike()
		p.line_width = 1
		
		p.set_color(col_text)
		p.draw_str(15, 15, text, 0.007)
		
	void set_state(int _state)
		if _state == state
			return
		state = _state
		draw()
		p.end()
		if state == BUTTON_STATE_CLICK
			onClick()
	virtual void onClick()
	override void onMouseMove()
		bool inside = r.inside(win.mx, win.my)
		if inside
			if win.lbut
				set_state(BUTTON_STATE_CLICK)
			else
				set_state(BUTTON_STATE_HOVER)
		if !inside
			set_state(BUTTON_STATE_DEFAULT)
		// not the best way :P

class MtkEdit extends MtkWidget
	string text
	int cursor_pos
	override void __init__()
		cursor_pos = 0
	void set_text(string text)
		self.text = text
		cursor_pos = text.num
		if p
			draw()
			p.end()
	override void draw()
		p.set_color(col_background)
		p.rectangle(p.all)
		p.fill()
		
		p.set_color(col_button_frame)
		p.line_width = 4
		p.rectangle(rect(3, r.w()-3, 3, r.h()-3))
		p.strike()
		p.line_width = 1
		
		int cx = p.get_str_width(text.substr(0, cursor_pos), 0.007)
		p.set_color(col_text)
		p.draw_str(10, 10, text, 0.007)
		p.move_to(10 + cx, 5)
		p.line_to(10 + cx, 20)
		p.strike()
	override void onKey(int key)
		if key == 0x7f
			if cursor_pos > 0
				text.remove(cursor_pos - 1)
				cursor_pos --
				notify_change()
		else if key >= 20
			string s
			s.add(key)
			text = text.head(cursor_pos) + s + text.tail(text.num - cursor_pos)
			//text.insert(cursor_pos, key)
			cursor_pos ++
			notify_change()
	void notify_change()
		if p
			draw()
			p.end()
		onEdit()
	virtual void onEdit()

class MtkWindow extends XWindow
	XPainter* p
	string title
	MtkWidget* widget
	MtkWidget*[] widgets
	bool shown
	bool dragging
	
	void __init__(string title, int w, int h)
		super.__init__(xcon, -1, -1, w + FRAME_WIDTH * 2, h + FRAME_WIDTH + FRAME_BAR)
		widget = nil
		shown = false
		dragging = false
		
		self.title = title
		
		p = new XPainter(self)
		p.font = LoadTTF("/home/x.font")
	
	override void __delete__()
		if widget
			delete widget
	
	void show()
		shown = true
		draw()
		p.end()
	
	void draw()
		shown = true
		draw_frame()
		if widget
			widget.draw()
	
	void set_title(string _title)
		if title == _title
			return
		title = _title
		if shown
			show()
	
	void put(MtkWidget* widget)
		self.widget = widget
		widget.set_win(self, FRAME_WIDTH, FRAME_BAR, w - FRAME_WIDTH*2, h - FRAME_WIDTH - FRAME_BAR)
		
		if shown
			widget.draw()
	
	void draw_frame()
		if active
			p.set_color(col_frame)
		else
			p.set_color(col_background)
		p.rectangle(p.all)
		p.fill()
		p.set_color(col_title)
		p.draw_str(FRAME_WIDTH, 12.0, title, 0.008)
		
	override void onMouseMove()
		for w in widgets
			w.mx = mx - w.r.x0
			w.my = my - w.r.y0
			w.onMouseMove()
			
	override void onMouseLeftButtonDown()
		//XWindow* ww = xcon.get_at_cursor()
		if self == xcon.cursor_win
			activate()
			if my < FRAME_BAR
				kprint("------ ")
				kprint(my)
				kprint("  -----")
				drag_start()
				dragging = true
			else
				for w in widgets
					w.onMouseLeftButtonDown()
					
	override void onMouseLeftButtonUp()
		if dragging
			drag_stop()
			dragging = false
		for w in widgets
			w.onMouseLeftButtonUp()
			
	override void onKey(int key)
		for w in widgets
			w.onKey(key)
	
	override void handleActivate()
		show()


class MtkIdle
	virtual void do()
		nil

XConnection* xcon
MtkIdle*[] idles

void mtk_init()
	init_lib()
	idles.__init__()
	
	col_frame = White
	col_text = Black
	col_background = color(1, 0.9, 0.9, 0.9)
	//col_background = color(1, 0.3, 0.7, 0.8)
	col_title = Black
	col_button = color(1, 0.6, 0.6, 0.95)
	col_button_frame = color(1, 0.4, 0.4, 0.80)
	col_button_hover = color(1, 0.7, 0.7, 1.0)
	col_button_hover_frame = color(1, 0.5, 0.5, 0.85)
	col_button_click = color(1, 0.4, 0.4, 0.8)
	col_button_click_frame = color(1, 0.2, 0.2, 0.5)
	
	xcon = new XConnection
	if xcon.error
		print("client: kann nicht mit x verbinden\n")
		exit(-1)

void mtk_add_idle(MtkIdle *i)
	idles.add(i)

void mtk_run()
	while true
		xcon.poll()
		usleep(5000)
		for i in idles
			i.do()
		
	delete xcon
	exit(0)