use "std.kaba"

const int XCMD_KILL = 666
const int XCMD_NEW_WINDOW = 1024
const int XCMD_DEL_WINDOW = 1025
const int XCMD_WINDOW_MOVE = 1026
const int XCMD_WINDOW_INVALIDATE = 1027
const int XCMD_WINDOW_GET_SIZE = 2048
const int XCMD_WINDOW_GET_POS = 2049
const int XCMD_WINDOW_DRAG_START = 2050
const int XCMD_WINDOW_DRAG_STOP = 2051
const int XCMD_WINDOW_ACTIVATE = 2052
const int XCMD_POINTER_GET_WINDOW = 3072

const int XCMD_EVENT_MOUSE = 3072
const int XCMD_EVENT_KEY = 3073
const int XCMD_EVENT_DRAW = 3074

const int CON_STATUS_CLEAR = 0
const int CON_STATUS_BUSY = 13
const int CON_STATUS_SERVER_SEND = 1
const int CON_STATUS_SERVER_ANSWER = 2
const int CON_STATUS_CLIENT_SEND = 3
const int CON_STATUS_CLIENT_ANSWER = 4

class XRect
	int x0, y0, x1, y1
	int w()
		return x1 - x0
	int h()
		return y1 - y0
	bool inside(int x, int y)
		return x >= x0 and x < x1 and y >= y0 and y < y1

class XWindow
	int x, y, w, h
	int id
	int[0]* buf
	XConnection *con
	
	bool active
	bool lbut, mbut, rbut
	int mx, my
	
	void __init__(XConnection *c, int x, int y, int w, int h)
		con = c
		buf = nil
		self.x = x
		self.y = y
		self.w = w
		self.h = h
		lbut = false
		mbut = false
		rbut = false
		mx = 0
		my = 0
		active = false
		c._create_win(self, x, y, w, h)
	void __delete__()
		if buf
			shmdt(buf)
	void create_buf(int _id, int key)
		id = _id
		int mem_id = shmget(key, 0, 0)
		buf = shmat(mem_id, 0)
		if !buf
			kprint("  -- client: konnte Fenster-Puffer nicht anlegen --\n")
	void invalidate(int x0, int y0, int x1, int y1)
		con.send(XCMD_WINDOW_INVALIDATE, id, x0, y0, x1, y1)
	void invalidate_all()
		con.send(XCMD_WINDOW_INVALIDATE, id, -1, -1, -1, -1)
	void move(int x, int y)
		con.send(XCMD_WINDOW_MOVE, id, x, y, 0, 0)
	void get_size(int *w, int *h)
		con.just_send(XCMD_WINDOW_GET_SIZE, id, 0, 0, 0, 0)
		con.wait_recv(XCMD_WINDOW_GET_SIZE)
		*w = con.buf[2]
		*h = con.buf[3]
	void activate()
		con.send(XCMD_WINDOW_ACTIVATE, id, 0, 0, 0, 0)
	void drag_start()
		con.just_send(XCMD_WINDOW_DRAG_START, id, 0, 0, 0, 0)
	void drag_stop()
		con.just_send(XCMD_WINDOW_DRAG_STOP, id, 0, 0, 0, 0)
	virtual void onKey(int key)
		//print("c-win: key " + key + "\n")
	virtual void onMouseMove()
	virtual void onMouseLeftButtonDown()
	virtual void onMouseLeftButtonUp()
	virtual void onMouseMiddleButtonDown()
	virtual void onMouseMiddleButtonUp()
	virtual void onMouseRightButtonDown()
	virtual void onMouseRightButtonUp()
		//print("c-win: mouse " + x + "/" + y + "   " + flags + "\n")
	virtual void handleActivate()
		

class XConnection
	int sock
	int[8] buf
	bool error
	XWindow*[] windows
	int mx, my
	bool lbut, mbut, rbut
	XWindow* cursor_win
	
	void __init__()
		error = true
		
		lbut = false
		mbut = false
		rbut = false
	
		// connect
		sock = socket(0)
		if !connect(sock, 0x7bcd0123)
			kprint("client: connect (2) fehlgeschlagen\n")
			return
		fcntl(sock, F_SETFL, O_RDWR | O_NONBLOCK)
	
		error = false
		
	void __delete__()
		if error
			return
		send_exit()
		close(sock)
	
	void just_send(int cmd, int p1, int p2, int p3, int p4, int p5)
		kprint(" c:send ")
		buf[0] = cmd
		buf[1] = p1
		buf[2] = p2
		buf[3] = p3
		buf[4] = p4
		buf[5] = p5
		kprint(write(sock, &buf[0], 4*8))
		kprint(" c:/send ")
	
	void send(int cmd, int p1, int p2, int p3, int p4, int p5)
		just_send(cmd, p1, p2, p3, p4, p5)
	
	void wait_recv(int cmd)
		kprint(" WAIT ")
		while true
			if poll()
				if buf[0] == cmd
					break
			usleep(100)
		kprint(" /WAIT ")

	void send_exit()
		send(XCMD_KILL, 0, 0, 0, 0, 0)

	bool _create_win(XWindow *win, int x, int y, int w, int h)
		kprint("client: send new win\n")
		just_send(XCMD_NEW_WINDOW, x, y, w, h, 0)
		
		wait_recv(XCMD_NEW_WINDOW)
		kprint("....client///  ")
		if buf[0] != XCMD_NEW_WINDOW
			kprint("   -- client: kein neues Fenster --\n")
			return false
		kprint("ok")
		int win_id = buf[1]
		int win_key = buf[2]
		win.create_buf(win_id, win_key)
		if !win.buf
			return false
		win.x = buf[3]
		win.y = buf[4]
		
		windows.add(win)
		//print "client: ok\n"// " + key + " " + id + "\n"
		return true
	
	/*XWindow* get_at_cursor()
		just_send(XCMD_POINTER_GET_WINDOW, 0, 0, 0, 0, 0)
		wait_recv(XCMD_POINTER_GET_WINDOW)
		int id = buf[1]
		for w in windows
			if w.id == id
				return w
		return nil*/
	
	bool poll()
		int n = read(sock, &buf, 4*8)
		if n < 32
			return false
		//kprint("  c:poll  ")
		if buf[0] == XCMD_EVENT_MOUSE
			//kprint("client: mouse\n")
			int x = buf[1]
			int y = buf[2]
			int flags = buf[3]
			int win_id = buf[4]
			cursor_win = nil
			for w in windows
				if w.id == win_id
					cursor_win = w
			kprint("MM " + x)
			kprint("/" + y)
			kprint(" - " + flags + "\n")
			bool new_lbut = (flags & 1) > 0
			bool new_mbut = (flags & 2) > 0
			bool new_rbut = (flags & 4) > 0
			for w in windows
				w.mx = x - w.x
				kprint("mmmm " + y + "  " + w.y + "  ")
				w.my = y - w.y
				w.lbut = new_lbut
				w.mbut = new_mbut
				w.rbut = new_rbut
				w.onMouseMove()
				if !lbut and new_lbut
					w.onMouseLeftButtonDown()
				if !mbut and new_mbut
					w.onMouseMiddleButtonDown()
				if !rbut and new_rbut
					w.onMouseRightButtonDown()
				if lbut and !new_lbut
					w.onMouseLeftButtonUp()
				if mbut and !new_mbut
					w.onMouseMiddleButtonUp()
				if rbut and !new_rbut
					w.onMouseRightButtonUp()
			lbut = new_lbut
			mbut = new_mbut
			rbut = new_rbut
			kprint(" /MM ")
		else if buf[0] == XCMD_EVENT_KEY
			//kprint("client: key\n")
			int key = buf[1]
			for w in windows
				if w.active
					w.onKey(key)
		else if buf[0] == XCMD_EVENT_DRAW
			//kprint("client: draw\n")
			nil
		else if buf[0] == XCMD_WINDOW_MOVE
			//kprint("client: move\n")
			int id = buf[1]
			for w in windows
				if w.id == id
					w.x = buf[2]
					w.y = buf[3]
		else if buf[0] == XCMD_WINDOW_ACTIVATE
			kprint(" activate  ")
			int id = buf[1]
			for w in windows
				bool old_active = w.active
				w.active = (w.id == id)
				if w.active != old_active
					w.handleActivate()
		
		kprint("  c:/poll  ")
		return true
	
	void run()
		while true
			poll()
			usleep(5000)
