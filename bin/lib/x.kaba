use lib.std.*
use lib.std.sys.*
use lib.std.net.*
use lib.std.file.*

const XCMD_KILL = 666
const XCMD_NEW_WINDOW = 1024
const XCMD_DEL_WINDOW = 1025
const XCMD_WINDOW_MOVE = 1026
const XCMD_WINDOW_INVALIDATE = 1027
const XCMD_WINDOW_GET_SIZE = 2048
const XCMD_WINDOW_GET_POS = 2049
const XCMD_WINDOW_DRAG_START = 2050
const XCMD_WINDOW_DRAG_STOP = 2051
const XCMD_WINDOW_ACTIVATE = 2052
const XCMD_POINTER_GET_WINDOW = 3072
const XCMD_EXECUTE = 4096

const XCMD_EVENT_MOUSE = 3072
const XCMD_EVENT_KEY = 3073
const XCMD_EVENT_DRAW = 3074

const CON_STATUS_CLEAR = 0
const CON_STATUS_BUSY = 13
const CON_STATUS_SERVER_SEND = 1
const CON_STATUS_SERVER_ANSWER = 2
const CON_STATUS_CLIENT_SEND = 3
const CON_STATUS_CLIENT_ANSWER = 4

class XRect
	var x0, y0, x1, y1: int
	func w() -> int
		return x1 - x0
	func h() -> int
		return y1 - y0
	func inside(x: int, y: int) -> bool
		return x >= x0 and x < x1 and y >= y0 and y < y1
	func __str__() -> string
		return "{{x0}}/{{y0}}:{{x1}}/{{y1}}"

class XWindow
	var x, y, w, h: int
	var id: int
	var buf: int[0]*
	var con: XConnection&
	
	var active: bool
	var lbut, mbut, rbut: bool
	var mx, my: int
	
	func __init__(out c: XConnection&, x: int, y: int, w: int, h: int)
		con := c
		buf = nil
		self.x = x
		self.y = y
		self.w = w
		self.h = h
		lbut = false
		mbut = false
		rbut = false
		mx = 0
		my = 0
		active = false
		c._create_win(&self, x, y, w, h)
	func virtual __delete__()
		for b in buf
			shmdt(&b)
		for i=>w in con.windows
			if w == &self
				con.windows.remove(i)
				break
	func mut create_buf(_id: int, key: int)
		id = _id
		let mem_id = shmget(key, 0, 0)
		buf = shmat(mem_id, 0)
		if !buf
			kprint("  -- client: konnte Fenster-Puffer nicht anlegen --\n")
	func mut invalidate(x0: int, y0: int, x1: int, y1: int)
		print("inval...")
		con.send(XCMD_WINDOW_INVALIDATE, id, x0, y0, x1, y1)
	func mut invalidate_all()
		print("inval all...")
		con.send(XCMD_WINDOW_INVALIDATE, id, -1, -1, -1, -1)
	func mut move(x: int, y: int)
		con.send(XCMD_WINDOW_MOVE, id, x, y, 0, 0)
	func mut get_size(out w: int, out h: int)
		con.just_send(XCMD_WINDOW_GET_SIZE, id, 0, 0, 0, 0)
		con.wait_recv(XCMD_WINDOW_GET_SIZE)
		w = con.buf[2]
		h = con.buf[3]
	func mut activate()
		con.send(XCMD_WINDOW_ACTIVATE, id, 0, 0, 0, 0)
	func mut drag_start()
		con.just_send(XCMD_WINDOW_DRAG_START, id, 0, 0, 0, 0)
	func mut drag_stop()
		con.just_send(XCMD_WINDOW_DRAG_STOP, id, 0, 0, 0, 0)
	func mut close()
		con.just_send(XCMD_DEL_WINDOW, id, 0, 0, 0, 0)
	func virtual mut on_key(key: int)
		#print("c-win: key " + key + "\n")
	func virtual mut on_mouse_move()
	func virtual mut on_mouse_left_button_down()
	func virtual mut on_mouse_left_button_up()
	func virtual mut on_mouse_middle_button_down()
	func virtual mut on_mouse_middle_button_up()
	func virtual mut on_mouse_right_button_down()
	func virtual mut on_mouse_right_button_up()
		#print("c-win: mouse " + x + "/" + y + "   " + flags + "\n")
	func virtual mut handle_activate()
		

class XConnection
	var sock: int
	var buf: int[8]
	var error: bool
	var windows: XWindow&[]
	var mx, my: int
	var lbut, mbut, rbut: bool
	var cursor_win: XWindow*
	
	func __init__()
		error = true
		
		lbut = false
		mbut = false
		rbut = false
	
		# connect
		sock = socket(0)
		if !connect(sock, 0x7bcd0123)
			kprint("client: connect (2) failed\n")
			return
		fcntl(sock, F_SETFL, O_RDWR | O_NONBLOCK)
	
		error = false
		
	func __delete__()
		if error
			return
		send_exit()
		close(sock)
	
	func mut just_send(cmd: int, p1: int, p2: int, p3: int, p4: int, p5: int)
		kprint(" c:send ")
		buf[0] = cmd
		buf[1] = p1
		buf[2] = p2
		buf[3] = p3
		buf[4] = p4
		buf[5] = p5
		var r = write(sock, &buf[0], 4*8)
		kprint(str(r))
		kprint(" c:/send ")
	
	func mut send(cmd: int, p1: int, p2: int, p3: int, p4: int, p5: int)
		just_send(cmd, p1, p2, p3, p4, p5)
	
	func mut send_string(s: string)
		write(sock, &s.num, 4)
		write(sock, &s[0], s.num)
	
	func mut wait_recv(cmd: int)
		kprint(" WAIT ")
		while true
			if poll()
				if buf[0] == cmd
					break
			usleep(100)
		kprint(" /WAIT ")

	func mut send_exit()
		send(XCMD_KILL, 0, 0, 0, 0, 0)

	func mut _create_win(out win: XWindow&, x: int, y: int, w: int, h: int) -> bool
		kprint("client: send new win\n")
		just_send(XCMD_NEW_WINDOW, x, y, w, h, 0)
		
		wait_recv(XCMD_NEW_WINDOW)
		kprint("....client#/  ")
		if buf[0] != XCMD_NEW_WINDOW
			kprint("   -- client: no new window --\n")
			return false
		kprint("ok")
		let win_id = buf[1]
		let win_key = buf[2]
		win.create_buf(win_id, win_key)
		if !win.buf
			return false
		win.x = buf[3]
		win.y = buf[4]
		
		windows.add(win)
		#print "client: ok\n"# " + key + " " + id + "\n"
		return true
	
	#XWindow* get_at_cursor()
#		just_send(XCMD_POINTER_GET_WINDOW, 0, 0, 0, 0, 0)
#		wait_recv(XCMD_POINTER_GET_WINDOW)
#		int id = buf[1]
#		for w in windows
#			if w.id == id
#				return w
#		return nil
	
	func mut execute(cmd: string[]) -> int
		just_send(XCMD_EXECUTE, cmd.num, 0, 0, 0, 0)
		for c in cmd
			send_string(c)
		
		wait_recv(XCMD_EXECUTE)
		return buf[1]
	
	func mut poll() -> bool
		let n = read(sock, &buf, 4*8)
		if n < 32
			return false
		#kprint("  c:poll  ")
		if buf[0] == XCMD_EVENT_MOUSE
			#kprint("client: mouse\n")
			let x = buf[1]
			let y = buf[2]
			let flags = buf[3]
			let win_id = buf[4]
			cursor_win = nil
			for w in windows
				if w.id == win_id
					cursor_win = w
			#kprint("MM " + x)
#			kprint("/" + y)
#			kprint(" - " + flags + "\n")
			let new_lbut = (flags & 1) > 0
			let new_mbut = (flags & 2) > 0
			let new_rbut = (flags & 4) > 0
			for mut w in windows
				if w != cursor_win
					continue
				w.mx = x - w.x
				#kprint("mmmm " + y + "  " + w.y + "  ")
				w.my = y - w.y
				w.lbut = new_lbut
				w.mbut = new_mbut
				w.rbut = new_rbut
				w.on_mouse_move()
				if !lbut and new_lbut
					w.on_mouse_left_button_down()
				if !mbut and new_mbut
					w.on_mouse_middle_button_down()
				if !rbut and new_rbut
					w.on_mouse_right_button_down()
				if lbut and !new_lbut
					w.on_mouse_left_button_up()
				if mbut and !new_mbut
					w.on_mouse_middle_button_up()
				if rbut and !new_rbut
					w.on_mouse_right_button_up()
			lbut = new_lbut
			mbut = new_mbut
			rbut = new_rbut
			#kprint(" /MM ")
		else if buf[0] == XCMD_EVENT_KEY
			#kprint("client: key\n")
			let key = buf[1]
			for mut w in windows
				if w.active
					w.on_key(key)
		else if buf[0] == XCMD_EVENT_DRAW
			#kprint("client: draw\n")
			pass
		else if buf[0] == XCMD_WINDOW_MOVE
			#kprint("client: move\n")
			let id = buf[1]
			for mut w in windows
				if w.id == id
					w.x = buf[2]
					w.y = buf[3]
		else if buf[0] == XCMD_WINDOW_ACTIVATE
			#kprint(" activate  ")
			let id = buf[1]
			for mut w in windows
				let old_active = w.active
				w.active = (w.id == id)
				if w.active != old_active
					w.handle_activate()
		
		#kprint("  c:/poll  ")
		return true
	
	func mut run()
		while true
			poll()
			usleep(5000)
