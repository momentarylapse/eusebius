use "std.kaba"
use "x.kaba"
use "ttfx.kaba"

int col2i(color c)
	int ir = c.r * 255.0
	int ig = c.g * 255.0
	int ib = c.b * 255.0
	int ia = c.a * 255.0
	return ib + (ig << 8) + (ir << 16) + (ia << 24)

void i2col(color c, int i)
	int ir = (i >> 16) & 255
	int ig = (i >> 8) & 255
	int ib = i & 255
	int ia = i >> 24
	c = color(i2f(ia) / 255.0, i2f(ir) / 255.0, i2f(ig) / 255.0, i2f(ib) / 255.0)

int add_blend(int i1, int i2, float t)
	float s = 1 - t
	color c1, c2
	i2col(c1, i1)
	i2col(c2, i2)
	return col2i(color(1, s*c1.r+t*c2.r, s*c1.g+t*c2.g, s*c1.b+t*c2.b))

class XCircle
	complex m
	float r

class XLine
	complex a, b

class XPainter
	int[0]* buf
	int width, height
	int buf_width
	int buf_x, buf_y
	rect all
	bool error
	
	int cur_color
	complex pos
	
	rect[] rects
	XCircle[] circles
	XLine[] path
	int last_path_start
	
	float line_width
	XWindow* win
	FontDescriptor* font
	
	void __init__(XWindow *_win)
		win = _win
		error = true
		buf = win.buf
		width = win.w
		height = win.h
		//win.get_size(&width, &height)
		/*if !win.get_size(&width, &height)
			print("Draw-Fehler: get_size()\n")
			return*/
		buf_width = width
		buf_x = 0
		buf_y = 0
		error = false
		
		font = nil
		set_color(Black)
		all = rect(0, width, 0, height)
		pos = complex(0, 0)
		line_width = 1.0
		last_path_start = 0
	
	void set_as_ref(XPainter* p, int x, int y, int w, int h)
		buf_width = p.buf_width
		buf_x = x
		buf_y = y
		buf = &p.buf[x + y * buf_width]
		width = w
		height = h
		
		//font = p.font
		//line_width = p.line_width
	
	void set_color(color c)
		cur_color = col2i(c)
	
	void rectangle(rect r)
		rects.add(r)
	
	void draw_pixel(int x, int y)
		if x < 0 or x >= width
			return
		if y < 0 or y >= height
			return
		int index = x + y * buf_width
		buf[index] = cur_color
	
	void draw_rect(rect r)
		int x0 = max(r.x1, 0)
		int y0 = max(r.y1, 0)
		int x1 = min(r.x2, width)
		int y1 = min(r.y2, height)
		for y in y0:y1
			int index = x0 + y * buf_width
			for x in x0:x1
				buf[index] = cur_color
				index ++
	
	void draw_circle(float mx, float my, float r)
		r += 0.5
		int x0 = max(mx - r, 0)
		int y0 = max(my - r, 0)
		int x1 = min(mx + r, width)
		int y1 = min(my + r, height)
		float rr_i = sqr(r - 1)
		float rr_o = sqr(r)
		for y in y0:y1
			int index = x0 + y * buf_width
			float dy = y - my
			float dx = x0 - mx
			for x in x0:x1
				float dd = dx*dx+dy*dy
				if dd < rr_i
					buf[index] = cur_color
				else if dd < rr_o
					buf[index] = add_blend(buf[index], cur_color, 0.5)
				dx += 1.0
				index ++
	
	void draw_line(complex a, complex b)
		float r = line_width * 1.5
		int x0 = max(min(a.x, b.x)-r, 0)
		int y0 = max(min(a.y, b.y)-r, 0)
		int x1 = min(max(a.x, b.x)+r, width)
		int y1 = min(max(a.y, b.y)+r, height)
		complex l = b - a
		float ll = (l.x*l.x + l.y*l.y)
		complex n = complex(l.y, -l.x)
		float f1_i = ll * sqr(line_width - 0.5) / 2
		float f1_o = ll * sqr(line_width + 0.5) / 2
		float f2 = ll * ll
		
		for y in y0:y1
			int index = x0 + y * buf_width
			float dy = y - a.y
			float dx = x0 - a.x
			for x in x0:x1
				float dn = n.x*dx + n.y*dy
				float dl = l.x*dx + l.y*dy
				if dl >= 0 and dl*dl < f2
					if dn*dn < f1_i
						buf[index] = cur_color
					else if dn*dn < f1_o
						buf[index] = add_blend(buf[index], cur_color, 0.5)
				dx += 1.0
				index ++
	
	void draw_spline(complex a, complex b, complex c)
		int n = 12
		complex z1 = a
		complex z2
		for i in 1:n
			float t = i2f(i) / n
			float s = 1 - t
			z2 = a * (s*s) + b * (s*t*2.0) + c * (t*t)
			draw_line(z1, z2)
			z1 = z2
		draw_line(z2, c)
	
	void fill_polygon()
		int[][] hit
		hit.resize(height)
		
		for l in path
			if l.a.y == l.b.y
				continue
			int y1 = l.a.y
			int y2 = l.b.y
			float m = (l.a.x - l.b.x) / (l.a.y - l.b.y)
			float a = l.a.x - l.a.y*m
			if y2 > y1
				y1 = max(y1, 0)
				y2 = min(y2, height)
				for y in y1:y2
					float hx = y*m + a
					hit[y].add(hx)
			else
				y1 = min(y1, height)
				y2 = max(y2, 0)
				for y in y2:y1
					int hx = y*m + a
					hit[y].add(hx)
		
		for y in 0:height
			if (hit[y].num & 1) != 0
				continue
			hit[y].sort()
			for i in 0:hit[y].num:2
				int x0 = max(hit[y][i], 0)
				int x1 = min(hit[y][i+1], width)
				int index = x0 + y * buf_width
				for x in x0:x1
					buf[index] = cur_color
					index ++
	
	void fill()
		for r in rects
			draw_rect(r)
		for c in circles
			draw_circle(c.m.x, c.m.y, c.r)
		fill_polygon()
		clear_data()
	
	void move_to(float x, float y)
		pos = complex(x, y)
		last_path_start = path.num
	
	void line_to(float x, float y)
		XLine l
		l.a = pos
		l.b = complex(x, y)
		path.add(l)
		pos = l.b
	
	void path_close()
		if path.num > last_path_start
			complex z = path[last_path_start].a
			line_to(z.x, z.y)
			last_path_start = path.num
			pos = z
	
	void clear_data()
		rects.clear()
		path.clear()
		circles.clear()
		last_path_start = 0
	
	void strike()
		for r in rects
			float d = line_width / 2
			draw_line(complex(r.x1, r.y1 - d), complex(r.x1, r.y2 + d))
			draw_line(complex(r.x1 - d, r.y2), complex(r.x2 + d, r.y2))
			draw_line(complex(r.x2, r.y2 + d), complex(r.x2, r.y1 - d))
			draw_line(complex(r.x2 + d, r.y1), complex(r.x1 - d, r.y1))
		for l in path
			draw_line(l.a, l.b)
		clear_data()
	
	void draw_glyph(float x, float y, TTFGlyph *g, float scale)
		if !font
			return
	
		TTFFont* f = font.font
		y -= scale * 0.2
		for c in g.contours
			move_to(x + f.points[c.start].x * scale, y + (1.0 - f.points[c.start].y) * scale)
			for i in c.start+1:c.start+c.num
				line_to(x + f.points[i].x * scale, y + (1.0 - f.points[i].y) * scale)
			path_close()
		strike()
		//fill()
	
	void draw_str(float x, float y, string text)
		float scale = font.size
		for c,i in text
			int code = c2i(c) & 0x000000ff
			
			if code == 9
				x += scale * 0.5 * 4
				continue
				
			TTFGlyph* g = font.font.get(code)
			if code != 32
				draw_glyph(x, y, g, scale)
			x += scale * g.width
	
	void draw_image(int _x, int _y, Image *im)
		int x0 = max(_x, 0)
		int y0 = max(_y, 0)
		int x1 = min(_x+im.width, width)
		int y1 = min(_y+im.height, height)
		for y in y0:y1
			int index = x0 + y * buf_width
			int _index = x0 - _x + (y - _y) * im.width
			for x in x0:x1
				buf[index] = im.data[_index]
				index ++
				_index ++
		
	void end()
		//win.invalidate_all()
		win.invalidate(buf_x, buf_y, buf_x + width, buf_y + height)
		
