use "std.kaba"
use "x.kaba"

int col2i(color c)
	int ir = c.r * 255.0
	int ig = c.g * 255.0
	int ib = c.b * 255.0
	int ia = c.a * 255.0
	return ib + (ig << 8) + (ir << 16) + (ia << 24)

void i2col(color c, int i)
	int ir = (i >> 16) & 255
	int ig = (i >> 8) & 255
	int ib = i & 255
	int ia = i >> 24
	c = color(i2f(ia) / 255.0, i2f(ir) / 255.0, i2f(ig) / 255.0, i2f(ib) / 255.0)

int add_blend(int i1, int i2, float t)
	float s = 1 - t
	color c1, c2
	i2col(c1, i1)
	i2col(c2, i2)
	return col2i(color(1, s*c1.r+t*c2.r, s*c1.g+t*c2.g, s*c1.b+t*c2.b))

class XPainter
	int[0]* buf
	int w, h
	bool error
	int cur_color
	rect cur_rect
	complex[] path
	bool path_closed
	float line_width
	XWindow *win
	
	void __init__(XWindow *_win)
		win = _win
		error = true
		buf = win.buf
		if !win.get_size(&w, &h)
			print "Draw-Fehler: get_size()\n"
			return
		error = false
		set_color(Black)
		set_rect(rect(0, w, 0, h))
		line_width = 1.0
		path_closed = false
	
	void set_color(color c)
		cur_color = col2i(c)
	
	void set_rect(rect r)
		cur_rect = r
	
	void draw_pixel(int x, int y)
		if x < 0 or x >= w
			return
		if y < 0 or y >= h
			return
		int index = x + y * w
		buf[index] = cur_color
	
	void fill()
		int x0 = max(cur_rect.x1, 0)
		int y0 = max(cur_rect.y1, 0)
		int x1 = min(cur_rect.x2, w)
		int y1 = min(cur_rect.y2, h)
		for y in y0:y1
			int index = x0 + y * w
			for x in x0:x1
				buf[index] = cur_color
				index ++
	
	void line(complex a, complex b)
		float r = line_width * 1.5
		int x0 = max(min(a.x, b.x)-r, 0)
		int y0 = max(min(a.y, b.y)-r, 0)
		int x1 = min(max(a.x, b.x)+r, w)
		int y1 = min(max(a.y, b.y)+r, h)
		complex l = b - a
		float ll = (l.x*l.x + l.y*l.y)
		complex n = complex(l.y, -l.x)
		float f1_i = ll * sqr(line_width - 0.5) / 2
		float f1_o = ll * sqr(line_width + 0.5) / 2
		float f2 = ll * ll
		
		for y in y0:y1
			int index = x0 + y * w
			float dy = y - a.y
			float dx = x0 - a.x
			for x in x0:x1
				float dn = n.x*dx + n.y*dy
				float dl = l.x*dx + l.y*dy
				if dl >= 0 and dl*dl < f2
					if dn*dn < f1_i
						buf[index] = cur_color
					else if dn*dn < f1_o
						buf[index] = add_blend(buf[index], cur_color, 0.5)
				dx += 1.0
				index ++
	
	void path_add(float x, float y)
		path.add(complex(x, y))
	
	void path_close()
		path_closed = true
	
	void path_clear()
		path.clear()
		path_closed = false
	
	void strike()
		if path.num > 0
			complex s = path[0]
			for d in path
				line(s, d)
				s = d
			if path_closed
				line(s, path[0])
		path_clear()
	
	void circle(float mx, float my, float r)
		r += 0.5
		int x0 = max(mx - r , 0)
		int y0 = max(my - r, 0)
		int x1 = min(mx + r, w)
		int y1 = min(my + r, h)
		float rr_i = sqr(r - 1)
		float rr_o = sqr(r)
		for y in y0:y1
			int index = x0 + y * w
			float dy = y - my
			float dx = x0 - mx
			for x in x0:x1
				float dd = dx*dx+dy*dy
				if dd < rr_i
					buf[index] = cur_color
				else if dd < rr_o
					buf[index] = add_blend(buf[index], cur_color, 0.5)
				dx += 1.0
				index ++
		
		
		
	void end()
		win.invalidate()
		
