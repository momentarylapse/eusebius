use os
use "lib/std.kaba"


string i2s2(int i, int n)
	string s = str(i)
	string r
	for j in len(s):n
		r.add(' ')
	return r + s


void ls(string dirname, bool extended)
	let l = Filesystem.search(dirname, "", true)
	if lib_error != 0
		print("ls: can not read directory: {{dirname}}\n")

	if extended
		for e in l
			if e.is_dir
				print("directory ")
			else
				print("file      ")
			print(i2s2(e.size, 8))
			print(" {{e.name}}\n")
	else
		for e,i in l
			if i > 0
				print("  ")
			print(e.name)
		print("\n")


void main()
	init_lib()
	lib_print_endl = false
	string[] arg
	get_args(arg)
	
	bool extended = false
	int n_shown = 0
	
	for i in 1:arg.num
		if arg[i] == "-l"
			extended = true
		else
			ls(arg[i], extended)
			n_shown ++
	if n_shown == 0
		ls(getcwd(), extended)
	
	exit(0)
