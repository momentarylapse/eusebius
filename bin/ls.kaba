use os
use lib.std


func ftype(path: Path) -> string
	if Filesystem.is_directory(path)
		return "d"
	else
		return "f"

func table_widths(list: string[], cols: int, space: int) -> int[]
	int[] ww
	for i in 0:cols
		int w = 0
		for s,k in list
			if (k%cols) == i
				w = max(w, len(s))
		if i < cols-1
			w += space
		ww.add(w)
	return ww

func optimal_table(list: string[], space: int, max_width: int) -> int[]
	int cols = len(list)
	while cols > 0
		var w = table_widths(list, cols, space)
		if w.sum() <= max_width
			return w
		cols --
	return [10]

func print_table(list: string[])
	var w = optimal_table(list, 2, 80)
	string o
	for s,i in list
		int col = (i % len(w))
		if col == 0 and i > 0
			o += "\n"
		o += s
		for j in len(s):w[col]
			o.add(' ')
	print(o)

func ls(dirname: Path, extended: bool)
	var list = Filesystem.search(dirname, "", "df")
	if lib_error != 0
		print("ls: can not read directory: {{dirname}}\n")

	if extended
		for e in list
			var path = dirname << e
			print("{{ftype(path)|-4}} {{Filesystem_size32(path)|8}} {{e}}\n")
	else
		print_table([for f in list str(f)])
		print("\n")

func main()
	init_lib()
	lib_print_endl = false
	string[] arg
	get_args(arg)
	
	bool extended = false

	Path[] paths

	for i in 1:arg.num
		if arg[i] == "-l"
			extended = true
		else
			paths.add(arg[i])
	for p in paths
		ls(p, extended)
	if len(paths) == 0
		ls(Filesystem.current_directory(), extended)

	exit(0)
