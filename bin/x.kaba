use "lib/std.kaba"
use "lib/io.kaba"
use "lib/pci.kaba"
use "lib/vesa.kaba"
//use "lib/x.kaba"


const int XCMD_KILL = 666
const int XCMD_WINDOW_NEW = 1024
const int XCMD_WINDOW_DELETE = 1025
const int XCMD_WINDOW_MOVE = 1026
const int XCMD_WINDOW_INVALIDATE = 1027
const int XCMD_WINDOW_GET_SIZE = 2048
const int XCMD_WINDOW_GET_POS = 2049

const int XCMD_EVENT_MOUSE = 3072
const int XCMD_EVENT_KEY = 3073
const int XCMD_EVENT_DRAW = 3074

const int CON_STATUS_CLEAR = 0
const int CON_STATUS_BUSY = 13
const int CON_STATUS_SERVER_SEND = 1
const int CON_STATUS_SERVER_ANSWER = 2
const int CON_STATUS_CLIENT_SEND = 3
const int CON_STATUS_CLIENT_ANSWER = 4

const int WF_TRANSPARENT = 1024
const int WF_DONT_INVALIDATE = 2048

class MouseController
	int x, y, flags
	XWindow* cursor
	
	int dev
	
	void __init__()
	
		x = screen_width / 2
		y = screen_height / 2
		dev = open("/dev/mouse", O_RDONLY | O_NONBLOCK)
		//fcntl(dev, F_SETFL, O_RDONLY | O_NONBLOCK)
		
	void create()

		Image *im = LoadImage("/images/cursor.tga")
	
		if im
			cursor = new XWindow(x - im.width/2, y - im.height/2, im.width, im.height, WF_TRANSPARENT | 7)
			cursor.fill_image(0, 0, im)
		
			delete im
		else
			cursor = new XWindow(x-16, y-16, 32, 32, WF_TRANSPARENT | 7)
			cursor.fill(0xffc0c0c0)

	bool poll_single()
		int[4] m
		if read(dev, &m, 4*4) < 16
			return false
		flags = m[0]
		x += m[1]
		y -= m[2]
		if x < 0
			x = 0
		if x >= screen_width
			x = screen_width - 1
		if y < 0
			y = 0
		if y >= screen_height
			y = screen_height - 1
	
		return true

	void poll()
		bool need_update = false
		for i in 0:8
			if !poll_single()
				break
			need_update = true
		if need_update
			for c in cmds.clients
				c.send_mouse()
			cursor.move(x - cursor.r.w()/2, y - cursor.r.h()/2)

MouseController* mouse

class KeyboardController
	int key
	void __init__()
		fcntl(stdin, F_SETFL, O_RDONLY | O_NONBLOCK)
	void poll()
		key = 0
		if read(stdin, &key, 1) <= 0
			return
		for c in cmds.clients
			c.send_key()

KeyboardController* keyboard

void XExit()
	BgaWriteRegister(VBE_DISPI_INDEX_ENABLE, VBE_DISPI_DISABLED)
	exit(0)

class XRect
	int x0, y0, x1, y1
	int w()
		return x1 - x0
	int h()
		return y1 - y0
	bool inside(int x, int y)
		return x >= x0 and x < x1 and y >= y0 and y < y1


class XWindow
	int id
	XRect r
	int shm_key
	int shm_id
	int[0]* buf
	bool transparent
	int level
	
	void __init__(int x, int y, int w, int h, int flags)
		kprint "x: new win\n"
		r.x0 = x
		r.y0 = y
		r.x1 = x + w
		r.y1 = y + h
		level = flags & 0xf
		transparent = ((flags & WF_TRANSPARENT) > 0)
		id = next_window_id
		next_window_id ++
		shm_key = next_window_id + 0x0f810000 // randi()
		shm_id = shmget(shm_key, w*h*4, SHM_DEST | IPC_CREAT)
		if shm_id < 0
			print "x: ERROR can't create shared memory for window\n"
			// TODO clean-up
		else
			buf = shmat(shm_id, 0)
			windows.add(self)
			sort_windows()
			if (flags & WF_DONT_INVALIDATE) == 0
				invalidated.add(r)
		
	void __delete__()
		kprint "x: del win\n"
		if buf
			invalidated.add(r)
			shmdt(buf)
		for ww,n in windows
			if ww == self
				windows_r.remove(windows.num - n - 1)
				windows.remove(n)
	
	void move(int x, int y)
		XRect r0 = r
		r.x1 += x - r.x0
		r.y1 += y - r.y0
		r.x0 = x
		r.y0 = y
		invalidated.add(r)
		invalidated.add(r0)
	
	//void draw_rect(int x, int y, int w, int h, int c)
	void fill(int c)
		int n = r.w() * r.h()
		for i in 0:n
			buf[i] = c
	void fill_image(int dx, int dy, Image *im)
		/*int x1 = dx
		if x1 < 0
			x1 = 0
			
		for y in y1:y2
			for x in x1:x2*/
		int w = r.w()
		int h = r.h()
		for y in 0:h
			for x in 0:w
				buf[x + y*w] = im.data[x + y*w]

int next_window_id
XWindow*[] windows, windows_r
XWindow* desktop

void sort_windows()
	for i in 0:windows.num
		for j in i+1:windows.num
			if windows[i].level > windows[j].level
				XWindow *t = windows[i]
				windows[i] = windows[j]
				windows[j] = t
	windows_r.resize(windows.num)
	for w, i in windows
		windows_r[windows.num - i - 1] = w

XRect[] invalidated

void draw(XRect rr)
	XRect r = rr
	if r.x0 < 0
		r.x0 = 0
	if r.x1 >= screen_width
		r.x1 = screen_width-1
	if r.y0 < 0
		r.y0 = 0
	if r.y1 >= screen_height
		r.y1 = screen_height-1
	for y in r.y0:r.y1
		int xs = r.x0
		while xs < r.x1
			int xe = r.x1
			
			// find the top most window at (x1,y)
			XWindow *ww = nil
			for w,i in windows_r
				if w.r.inside(xs, y)
					ww = w
					if w.r.x1 < xe
						xe = w.r.x1
					// test for overlap in [y]*[xs:xe]
					for j in 0:i
						XWindow *v = windows_r[j]
						if y < v.r.y0 or y >= v.r.y1
							continue
						if xe <= v.r.x0 or xs >= v.r.x1
							continue
						xe = v.r.x0
					break
			if !ww
				xs = xe
				continue
			if ww.transparent
				// brute force :P
				for x in xs:xe
					for w in windows_r
						if w.r.inside(x, y)
							int dx = x-w.r.x0
							int dy = y-w.r.y0
							int c = w.buf[dx + dy*w.r.w()]
							if w.transparent
								if (c & 0xff000000) != 0
									setPixel(x, y, c)
									break
							else
								setPixel(x, y, c)
								break
			else
				int dy = y-ww.r.y0
				int dx = xs-ww.r.x0
				int index = dx + dy*ww.r.w()
				for x in xs:xe
					setPixel(x, y, ww.buf[index])
					index ++
			
			xs = xe
	

void draw_invalidated()
	for r in invalidated
		draw(r)
	invalidated.clear()


class Client
	int[0]* buf
	bool kill_me
	XWindow*[] win
	int key
	
	void __init__(int _key)
		key = _key
		kill_me = false
		kprint "x: new client key=" + key + " \n"
		int id = shmget(key, 0, 0)
		buf = shmat(id, 0)
		if !buf
			kprint "  -- x: Fehler shmat (2) --\n"
	
	void __delete__()
		kprint "x: del client\n"
		shmdt(buf)
		for w in win
			delete w
	
	void wait_clear()
		kprint " x:wc "
		while buf[0] != CON_STATUS_CLEAR
			poll()
		kprint " x:/wc "
	
	void __wait_clear_poll()
		while buf[0] != CON_STATUS_CLEAR
			poll()
	
	void clear()
		kprint " x:clear "
		buf[0] = CON_STATUS_CLEAR
	
	void just_send(int cmd, int p1, int p2, int p3, int p4, int p5)
		kprint " x:send "
		wait_clear()
		buf[0] = CON_STATUS_BUSY
		buf[1] = cmd
		buf[2] = p1
		buf[3] = p2
		buf[4] = p3
		buf[5] = p4
		buf[6] = p5
		buf[0] = CON_STATUS_SERVER_SEND
		wait_clear()
		//return buf[32]
	
	void send(int cmd, int p1, int p2, int p3, int p4, int p5)
		just_send(cmd, p1, p2, p3, p4, p5)
		clear()
	
	void send_mouse()
		kprint "x: send mouse\n"
		send(XCMD_EVENT_MOUSE, mouse.x, mouse.y, mouse.flags, 0, 0)
	
	void send_key()
		kprint "x: send key\n"
		send(XCMD_EVENT_KEY, keyboard.key, 0, 0, 0, 0)
	
	void poll()
		if buf[0] != 0
			kprint "    x:" + buf[0]
		if buf[0] != CON_STATUS_CLIENT_SEND
			return
		buf[0] = CON_STATUS_BUSY
		buf[32] = -1
		kprint "x: empfange\n"
		int end_status = CON_STATUS_CLEAR
		if buf[1] == XCMD_KILL
			buf[32] = 0
			kill_me = true
		else if buf[1] == XCMD_WINDOW_NEW
			XWindow* ww = new XWindow(buf[2], buf[3], buf[4], buf[5], 2 | WF_DONT_INVALIDATE)
			win.add(ww)
			buf[32] = 0
			buf[33] = ww.id
			buf[34] = ww.shm_key
			kprint "x: c.key=" + key + " w.key=" + ww.shm_key + "\n"
			end_status = CON_STATUS_SERVER_ANSWER
		else if buf[1] == XCMD_WINDOW_INVALIDATE
			for w in win
				if w.id == buf[2]
					invalidated.add(w.r)
					buf[32] = 0
		else if buf[1] == XCMD_WINDOW_MOVE
			for w in win
				if w.id == buf[2]
					w.move(buf[3], buf[4])
					buf[32] = 0
		else if buf[1] == XCMD_WINDOW_GET_POS
			for w in win
				if w.id == buf[2]
					buf[32] = 0
					buf[33] = w.r.x0
					buf[34] = w.r.y0
					break
		else if buf[1] == XCMD_WINDOW_GET_SIZE
			for w in win
				if w.id == buf[2]
					buf[32] = 0
					buf[33] = w.r.w()
					buf[34] = w.r.h()
					break
		buf[0] = end_status
		

class CommandController
	int[0]* buf
	//int connection
	Client*[] clients
	
	void __init__()
	
		//mkfifo("/tmp/x")
		//connection = open("/tmp/x", O_RDONLY)
		int id = shmget(0x7bcd0123, 1024, SHM_DEST | IPC_CREAT)
		buf = shmat(id, 0)
		if !buf
			print "x: Fehler shmat (1)\n"
			exit(-1)
		
		buf[0] = CON_STATUS_CLEAR
	
	void update()
		//kprinti buf[0]
		if buf[0] == CON_STATUS_CLIENT_SEND
			buf[0] = CON_STATUS_BUSY
			//kprint "  x:new client "
			clients.add(new Client(buf[1]))
			buf[0] = CON_STATUS_CLEAR
			
		for c in clients
			c.poll()
		for i in 0:clients.num
			if clients[i].kill_me
				delete clients[i]
				clients.remove(i)
				i --

CommandController *cmds

void main()
	init_lib()
	string[] arg
	get_args(arg)
	
	vesa_init()

	next_window_id = 0
	windows.__init__()
	windows_r.__init__()
	invalidated.__init__()
	
	
	
	cmds = new CommandController
	mouse = new MouseController
	keyboard = new KeyboardController
	
	if arg.num == 1
		BgaSetVideoMode(800, 600, 32)
		mouse.x = screen_width / 2
		mouse.y = screen_height / 2
	
	desktop = new XWindow(0, 0, screen_width, screen_height, 1)
	if vga_mem2
		desktop.fill(0xff402040)
	else
		desktop.fill(0xff000000)
	
	mouse.create()
	
	draw_invalidated()
	
	// make our debugging life easier...
	execute(["/bin/xterm"], -1, -1)
	//execute(["/bin/c"], -1, -1)
	
	print "x: running\n"
	while true
		cmds.update()
		mouse.poll()
		keyboard.poll()
		draw_invalidated()
		usleep(5000)
		
	XExit()
