use "lib/std.kaba"
use "lib/mtk/mtk.kaba"
use "lib/draw.kaba"
use os
use hui
use image

class FilesView extends MtkWidget
	string dir
	string[] list
	void __init__(string dir)
		set_dir(dir)
		snd = MtkSizeNegotiationData(30, 30, true, true)
	
	override bool can_grab_input()
		return true
	
	void set_dir(string _dir)
		dir = _dir
		if dir[dir.num-1] == '/'
			dir.remove(dir.num-1)
		if dir == ""
			dir = "/"
		list = Filesystem.search(dir, "*", true)
		if win
			win.set_title(dir)
			win.invalidate_all()
		#p.end()
	
	override void draw()
		if win.active
			p.set_color(col_window_frame)
		else
			p.set_color(col_background)
		p.rectangle(p.all)
		p.fill()
		
		for e,i in list
			if Filesystem.is_directory(dir + "/" + e)
				p.draw_image(10, 10 + i * 20, icon_dir)
			else
				p.draw_image(10, 10 + i * 20, icon_file)
			p.set_color(col_text)
			p.draw_str(30, 10 + i * 20, e)
		#p.draw_str(200, 10, dir)
	
	override void on_mouse_move()
	override void on_mouse_left_button_down()
		for e,i in list
			if mx >= 10 and mx < 200 and my >= 10+i*20 and my < 30+i*20
				if Filesystem.is_directory(dir + "/" + e)
					set_dir(dir_append(dir, e))
					break
				else
					mtk_open(dir_append(dir, e))
	override void on_mouse_left_button_up()
	override void on_key(int key)
		if key == KEY_BACKSPACE
			if dir.num > 2
				string[] parts = dir.explode("/")
				string d
				for i in 1:parts.num-1
					d += "/"
					d += parts[i]
				set_dir(d)
	override void on_resize()
		win.set_title(dir)
		

class FilesWindow extends MtkWindow
	FilesView* v
	void __init__(string dir)
		super.__init__("XFiles", 600, 400)
		
		v = new FilesView(dir)
		put(v)
		
		

FilesWindow* window
Image* icon_dir, icon_file

void main()
	mtk_init()
	string[] arg
	get_args(arg)
	
	icon_dir = new Image(16, 16, color(1, 0.5, 0.5, 0))
	icon_file = new Image(16, 16, color(1, 0.8, 0.8, 0.8))
	
	if arg.num > 1
		window = new FilesWindow(arg[1])
	else
		window = new FilesWindow("/")
	window.show()

	mtk_run()
	
