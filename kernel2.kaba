#define __OS__
#define __CODE_ORIGIN__ 0x10000
#define __VARIABLE_OFFSET__ 0x110010 // > ~1mb, wegen Stack!
#define __ADD_ENTRY_POINT__
#define __STRING_CONST_AS_CSTRING__
//#disasm

int MemorySize


//--------------------------------------------------------------
// Low Level

int _temp_port_no_
int _temp_port_data_

void outportb(int port, char data)
	_temp_port_no_ = port
	_temp_port_data_ = data
	asm{
		mov eax, _temp_port_data_
		mov edx, _temp_port_no_
		out dx, al
	}

void outportd(int port,int data)
	_temp_port_no_ = port
	_temp_port_data_ = data
	asm{
		mov eax, _temp_port_data_
		mov edx, _temp_port_no_
		out dx, eax
	}

char inportb(int port)
	_temp_port_no_ = port
	asm{
		mov edx, _temp_port_no_
		xor eax, eax
		in al, dx
		mov _temp_port_data_, eax
	}
	return _temp_port_data_

int inportd(int port)
	_temp_port_no_ = port
	asm{
		mov edx, _temp_port_no_
		in eax, dx
		mov _temp_port_data_, eax
	}
	return _temp_port_data_



void read_data_from_init()
	asm{
		mov eax, [0x00005100]
		mov MemorySize, eax
	}


void memcpy(cstring *dest, cstring *source, int size)
	for int n, 0, size
		dest[n] = source[n]
	/*if size > 0
		temp_i=size
		temp_p=source
		temp_p2=dest
		asm{
			mov ecx, temp_i
			mov eax, temp_p
			mov ebx, temp_p2
			_mem_cpy_loop:
				mov.b dl, [eax]
				mov.b [ebx], dl
				inc eax
				inc ebx
				loop _mem_cpy_loop
		}*/

int strlen(cstring *s)
	for int i, 0, 256
		if s[i] == 0
			return i
	return 256

void strcpy(cstring *a, cstring *b)
	for int i, 0, 256
		a[i] = b[i]
		if b[i] == 0
			break

void strcat(cstring *a, cstring *b)
	int n = strlen(a)
	strcpy(&a[n], b)

int strcmp(cstring *a, cstring *b)
	for int i, 0, 256
		if a[i] > b[i]
			return 1
		else if a[i] < b[i]
			return -1
		if a[i] == 0
			break
	return 0

int __temp_i__, __temp_i2__, __temp_i3__, __temp_i4__
void *__temp_p__, *__temp_p2__, *__temp_p3__, *__temp_p4__
int _p2i(void *p)
	int *i = &p
	return *i

class PointerWrapper
	void *p

void *_i2p(int i)
	PointerWrapper *p = &i
	return p.p


//--------------------------------------------------------------
// Speicher-"Verwaltung"	

const int MEM_ALLOC_OFFSET = 0x00400000 // (4-2)=2mb Kernel-Variablen
const int MAX_MEM_ALLOCS = 4096

int NumMemAllocs
class mem_alloc_t
	int Offset,End
mem_alloc_t MemAlloc[MAX_MEM_ALLOCS]


// Speicherblöcke bleiben immer so sortiert, dass der Offset monoton mit dem Index steigt!
void *malloc(int size)
	// Anfang
	int offset=MEM_ALLOC_OFFSET
	int a=0
	// suchen
	while a<NumMemAllocs
		if offset+size<MemAlloc[a].Offset
			break
		offset=MemAlloc[a].End
		a ++
	// hintere Allocs verschieben
	int a2=NumMemAllocs-1
	while a2>=a
		MemAlloc[a2+1]=MemAlloc[a2]
		a2--
	// neuen Alloc einfuegen
	MemAlloc[a].Offset=offset
	MemAlloc[a].End=offset+size
	NumMemAllocs++
	return _i2p(offset)

void free(void *mem)
	int mem_i=_p2i(mem)
	int a=0
	int ae=-1
	while (ae<0) or (a<NumMemAllocs)
		if MemAlloc[a].Offset==mem_i
			ae=a
		a++
	if ae>=0
		a=ae
		NumMemAllocs--
		while a<NumMemAllocs
			MemAlloc[a]=MemAlloc[a+1]
			a++
	

//--------------------------------------------------------------
// Text Modus

void *VideoMemory
char TextColor, TextColor2
int TextPos, TextLine
const int SCREEN_WIDTH = 80
const int SCREEN_HEIGHT = 25

void clrscr()
	cstring *d = VideoMemory
	for int i, 0, SCREEN_WIDTH * SCREEN_HEIGHT
		d[i * 2] = ' '
		d[1 * 2 + 1] = 0
	TextLine = 0
	TextPos = 0

void etm_init()
	asm{
		mov eax, 0x000b8000
		mov VideoMemory, eax
	}
	clrscr()
	TextColor = 7

void etm_shift()
	cstring *d = VideoMemory
	cstring *d2 = &d[SCREEN_WIDTH * 2]
	for int i, 0, SCREEN_WIDTH * (SCREEN_HEIGHT - 1) * 2
		d[i] = d2[i]
	d2 = &d[SCREEN_WIDTH * (SCREEN_HEIGHT - 1) * 2]
	for int i, 0, SCREEN_WIDTH
		d2[i * 2] = ' '
		d2[i * 2 + 1] = 0

void prints_low(cstring *str, int size)
	cstring *d = VideoMemory
	d = &d[2*(SCREEN_WIDTH * TextLine + TextPos)]
	for int i, 0, size
		if str[i] != 0x0a
			d[0] = str[i]
			d[1] = TextColor
			d = &d[2]
			TextPos ++
		if (TextPos >= SCREEN_WIDTH) or (str[i] == 0x0a)
			TextPos = 0
			TextLine ++
			if TextLine >= SCREEN_HEIGHT
				etm_shift()
				TextLine --
			d = VideoMemory
			d = &d[2*(SCREEN_WIDTH * TextLine)]
	int position = (TextLine * SCREEN_WIDTH) + TextPos
	// cursor LOW port to vga INDEX register
	outportb(0x3d4, 0x0f)
	outportb(0x3d5, position)
	// cursor HIGH port to vga INDEX register
	outportb(0x3d4, 0x0e)
	outportb(0x3d5, position >> 8)

void prints(cstring str)
	prints_low(&str, strlen(&str))

void _i2s(int i, cstring *s)
	bool sign = (i >= 0)
	if !sign
		i = -i
	char ttt[16]
	ttt[15] = 0
	int offset = 14
	while true
		ttt[offset] = (i % 10) + 0x30
		i /= 10
		offset --
		if i == 0
			break
	if !sign
		ttt[offset] = '-'
		offset --
	strcpy(s, &ttt[offset + 1])

void printi(int i)
	cstring s
	_i2s(i, &s)
	prints(s)

int printh(cstring *data,int size)
	cstring ttt
	for int i, 0, size
		int d = data[i]
		int d1 = (d>>4)
		int d2 = (d&15)
		if d1 < 10
			ttt[0] = d1+48
		else
			ttt[0] = d1+87
		if d2<10
			ttt[1] = d2+48
		else
			ttt[1] = d2+87
		if i == size-1
			ttt[2] = 0
		else
			ttt[2] = '.'
			ttt[3] = 0
		prints(ttt)

/*void WasteTime()
	int x
	for x, 0, 100000
		nil*/


//--------------------------------------------------------------
// Ata Treiber

#define AtaMaskBusy		0x80
#define AtaMaskReady		0x40
#define AtaMaskData		0x08

class AtaDiskInfo
	bool isAvailable
	bool isAtapi
	int size
	cstring name

class AtaData

	int nAta
	int Status
	int IOBase, IOCtrl, ide_no, ide_ms
	char MSMask

	char Buffer[512]
	int BufferDW[0]*
	int Size

	char atapi_buffer[16]
	
	AtaDiskInfo DiskInfo[4]

	void wait_400_ns()
		for int i, 0, 100
			inportb(IOBase + 7)

	bool wait_status(char mask, char value)
		char AtaStatusChar = inportb(IOBase + 7)
		int n = 1000000000
		while ((AtaStatusChar & mask)!=value) and (n >= 0)
			AtaStatusChar = inportb(IOCtrl)//+7)
			n--
		return n >= 0

	void SetDiskAdd(int disk_no)
		ide_no = (disk_no/2)
		ide_ms = (disk_no%2)
		// Adressierungs-Daten
		IOBase = 0x1f0
		if ide_no == 1
			IOBase = 0x170
		IOCtrl = IOBase + 0x206
		//IOCtrl = 0x3f6
		MSMask = 0x00
		if ide_ms == 1
			MSMask = 0x10

	bool TestDisk(int disk_no)
		SetDiskAdd(disk_no)
		DiskInfo[disk_no].isAvailable = false

		// generell ansprechbar?
		prints("IDE ")
		printi(ide_no)
		prints(":")
		printi(ide_ms)
		TextPos = 20
		outportb(0x1f6, MSMask)
		wait_400_ns()
		outportb(IOBase + 2, 0x55)
		outportb(IOBase + 3, 0xaa)
		outportb(IOBase + 2, 0xaa)
		outportb(IOBase + 3, 0x55)
		outportb(IOBase + 2, 0x55)
		outportb(IOBase + 3, 0xaa)
		char sc = inportb(IOBase + 2)
		char sn = inportb(IOBase + 3)
		if (sc != 0x55) or (sn != 0xaa)
			prints("---\n")
			return false

		// now we think we know which devices, if any are there,
		// so lets try a soft reset (ignoring any errors).

		outportb(IOBase + 6, MSMask)
		wait_400_ns()
		outportb(IOCtrl, 0x0e) // soft reset
		wait_400_ns()
		outportb(IOCtrl, 0x0a)
		wait_400_ns()

		//sub_atapi_delay( 0 )
		wait_status(AtaMaskBusy, 0)
		outportb(IOBase + 6, MSMask)
		wait_400_ns()

		// lets check device 0 again, is device 0 really there?
		// is it ATA or ATAPI?
		sc = inportb(IOBase + 2)
		sn = inportb(IOBase + 3)
		bool fail = true
		if (sc == 0x01) and (sn == 0x01)
			DiskInfo[disk_no].isAtapi = false
			fail = false
			char cl = inportb(IOBase + 4)
			char ch = inportb(IOBase + 5)
			char st = inportb(IOBase + 7)
			if ( cl == 0x14 ) and (ch == 0xeb)
				prints("ATAPI (CD-Rom)")
				DiskInfo[disk_no].isAtapi = true
			else
				if (cl == 0x00) and (ch == 0x00) and (st != 0x00)
					prints("ATA")
				else
					fail = true
		if fail
			prints("---\n")
			return false

		/*outportb( 0x1f6, 0xe0 ) // Master+LBA
		wait_400_ns()
		outportb( 0x3f6, 0x0e ) // ?+SoftReset+NoInt
		wait_400_ns()
		outportb( 0x3f6, 0x0a ) // ?+NoInt
		wait_400_ns()
		ata_wait_busy()*/

		DiskInfo[disk_no].isAvailable = true

		outportb(IOCtrl, 0x0a) // ?+NoInt
		wait_400_ns()
		wait_status(AtaMaskBusy, 0)
		outportb(IOBase+1, 0) //?
		outportb(IOBase+2, 0) // NumSectors
 		outportb(IOBase+3, 0) // S0
 		outportb(IOBase+4, 0) // C0
		outportb(IOBase+5, 0)
		char ata_lba3 = MSMask | 0x60 // ...+LBA
		outportb(IOBase+6, ata_lba3) // H0
		if DiskInfo[disk_no].isAtapi
			outportb(IOBase+7, 0xa1) // IdentifyDrive (ATAPI)
		else
			outportb(IOBase+7, 0xec) // IdentifyDrive
		wait_400_ns()

		wait_status(0x88, AtaMaskData)
		wait_400_ns()

		BufferDW = &Buffer
		int n
		for n, 0, 128
			BufferDW[n] = inportd(IOBase)
		// die Bytes des Namens korregieren
		//for n, 0, 40, 2
		n=0
		while n<40
			DiskInfo[disk_no].name[n  ] = Buffer[55+n]
			DiskInfo[disk_no].name[n+1] = Buffer[54+n]
			n+=2
		DiskInfo[disk_no].name[40] = 0
		prints(DiskInfo[disk_no].name)
		memcpy(&DiskInfo[disk_no].size, &Buffer[120], 4)
		/*temp_i = Ata.DiskInfo[disk_no].size >> 11
		asm{
			mov eax, temp_i
			shr eax, 0xb
			mov temp_i, eax
		}*/
		TextPos = 60
		printi(DiskInfo[disk_no].size >> 11)
	//	TextPos=60
	//	printi(DiskInfo[disk_no].Size)
		prints("mb\n")

		return true

	bool Reset(int disk_no)
		if !DiskInfo[disk_no].isAvailable
			return false
		prints("AtaReset ")
		SetDiskAdd(disk_no)
	
		char status
		status=inportb(IOBase+7)
		printh(&status,1)
		status=inportb(IOBase+1)
		printh(&status,1)
		status=inportb(IOCtrl)
		printh(&status,1)
	
		status=inportb(IOBase+7)
		printh(&status,1)
		status=inportb(IOBase+1)
		printh(&status,1)
		status=inportb(IOCtrl)
		printh(&status,1)
		prints("\n")

	void Init()
		prints("ATA Geraete:\n")
	
		// set up Device Control register
		outportb(0x3f6, 0x0a)
		outportb(0x376, 0x0a)

		for int n, 0, 4
			TestDisk(n)
			
	// 1 Sektor = 512 b
	bool ReadSector(int disk_no, int sector_offset, int num_sectors, char *buf)
		if !DiskInfo[disk_no].isAvailable
			return false
		//DrawString(0,24,"MAtaRead")
		SetDiskAdd(disk_no)
		//printh(&IOBase,2)
		// num_sectors=1...
		outportb(IOBase + 0x206, 0x0a)
	
		char status


		if DiskInfo[disk_no].isAtapi
			nil
			/*
			/*status=inportb(AtaIOBase+7)
			DrawHex(20,24,&status,1)
			status=inportb(AtaIOBase+1)
			DrawHex(23,24,&status,1)
			status=inportb(AtaIOCtrl)
			DrawHex(26,24,&status,1)
			WaitReturn()*/
			
			wait_status(0x88, 0)
			outportb(IOBase + 7, 0x08) // Soft Reset
			wait_400_ns()

			// CommandPacket
			atapi_buffer[0]=0xa8 // Read
			atapi_buffer[1]=0x00
			atapi_buffer[2]=i2c(sector_offset>>24) // LBA 24..31
			atapi_buffer[3]=i2c(sector_offset>>16) // 16..23
			atapi_buffer[4]=i2c(sector_offset>>8) // 8..15
			atapi_buffer[5]=i2c(sector_offset) // 0..7
			atapi_buffer[6]=0x00
			atapi_buffer[7]=0x00
			atapi_buffer[8]=0x00
			atapi_buffer[9]=i2c(num_sectors) // NumSectors
			atapi_buffer[10]=0x00
			atapi_buffer[11]=0x00
		
			//wait_status(AtaMaskBusy | AtaMaskData,0)
			wait_status(0x88,0)

			// dem ATA-Controller sagen, wir wollen ein ATAPI-Paket senden
			outportb(IOBase+1, 0 ) // Features
			outportb(IOBase+2, 0 )
			outportb(IOBase+3, 0 )
			outportb(IOBase+4, 0 )
			outportb(IOBase+5, 0 )
			outportb(IOBase+6, MSMask | 0x40 ) // H0 ...+LBA
			outportb(IOBase+7, 0xa0 ) // Packet
			wait_400_ns()

			//ata_wait_status(AtaMaskBusy,0)

			//ata_wait_status(0x88,AtaMaskData)
			wait_status(MaskData,AtaMaskData)

			/*status=inportb(AtaIOBase+7)
			DrawHex(50,15,&status,1)
			status=inportb(AtaIOBase+1)
			DrawHex(50,16,&status,1)
			status=inportb(AtaIOCtrl)
			DrawHex(50,17,&status,1)*/
		
		
			int abdw[0]*
			abdw=&atapi_buffer
			outportd(AtaIOBase,abdw[0])
			outportd(AtaIOBase,abdw[1])
			outportd(AtaIOBase,abdw[2])*/
		else
			outportb(IOCtrl, 0x0a) // ?+NoInt
			wait_400_ns()
			wait_status(AtaMaskBusy, 0)

			outportb(IOBase+1, 0) // Features
			outportb(IOBase+2, num_sectors) // NumSectors
 			outportb(IOBase+3, sector_offset) // S0
 			outportb(IOBase+4, sector_offset >> 8) // C0
			outportb(IOBase+5, sector_offset >> 16)
			outportb(IOBase+6, sector_offset >> 24 | MSMask | 0x40 ) // H0 ...+LBA
			outportb(IOBase+7, 0x20 ) // ReadSector
		wait_400_ns()

		wait_status(0x88, AtaMaskData)
		//wait_status(AtaMaskData | AtaMaskBusy,AtaMaskData)
		wait_400_ns()

		BufferDW = buf
	
		if DiskInfo[disk_no].isAtapi
			//prints("Lese...")
			status=inportb(IOBase+7)
			//printh(&status,1)
			status=inportb(IOBase+1)
			//printh(&status,1)
			status=inportb(IOCtrl)
			//printh(&status,1)
		
			int read=0
			while read<512 // 2048b
		
				int byte_cnt = inportb(IOBase + 5) * 256 + inportb(IOBase + 4)
				printi(byte_cnt)
				byte_cnt=20
		
				int dw_cnt=byte_cnt/4
				int n
				for n, 0, dw_cnt
					BufferDW[n]=inportd(IOBase)
				printh(buf,20)
				read+=dw_cnt
		else
			for n, 0, 128 // 512b
				BufferDW[n] = inportd(IOBase)

		status=inportb(IOBase+7)
		//printh(&status,1)
		status=inportb(IOBase+1)
		//printh(&status,1)
		status=inportb(IOCtrl)
		//printh(&status,1)

		return true

AtaData Ata


class PartitionData
	int offset, size
	char type
	bool active
	
PartitionData Partition[4]
int CurrentPartition

void ReadPartitions(int disk_no)
	// mbr lesen
	Ata.ReadSector(disk_no,0,1,&Ata.Buffer)
	//MountPartition=false
	for int n, 0, 4
		PartitionData *p = &Partition[n]
		int *offset = &Ata.Buffer[454+n*16]
		p.offset = *offset / 4
		int *size = &Ata.Buffer[458+n*16]
		p.size = *size / 4
		p.type = Ata.Buffer[450+n*16]
		p.active = (Ata.Buffer[446+n*16] == 0x80)
	/*for n, 0, 512
		DrawHex(n*3,2,&AtaBuffer[n],1)*/
		
void ShowPartitions()
	prints("Primaere Partitionen:     Offset   Groesse\n")
	for int n, 0, 4
		PartitionData *p = &Partition[n]
		prints("/dev/hda")
		printi(n + 1)
		if (p.type != 0x00) and (p.size > 0)
			TextPos = 60
			if p.active
				prints("(aktiv)")
			TextPos = 27
			printi(p.offset)
			TextPos = 36
			printi(p.size)
			TextPos = 50
			if p.type == 0x83
				prints("Linux")
			else if p.type == 0x20
				prints("MichiFS")
			else if p.type == 0x0c
				prints("Fat 32")
			else if p.type == 0x0e
				prints("Fat 16")
			else if p.type == 0x07
				prints("NTFS")
			else if p.type == 0x0f
				prints("Erweitert...")
			else
				printh(&p.type, 1)
		else
			TextPos = 50
			prints("---")
		prints("\n")

//--------------------------------------------------------------
// Dateisystem

const int FILE_TYPE_FILE = 0
const int FILE_TYPE_DIR = 1
const int FILE_TYPE_CHAR = 8
const int FILE_TYPE_BLOCK = 16


class FileOperations
	void *_open
	void *_close
	void *_read
	void *_write

class FileEntry
	cstring name
	int type
	int size
	int offset
	int num_clusters
	FileEntry *next
	FileEntry *first_sub
	FileOperations *op
	
	void add(FileEntry *e)
		if first_sub
			FileEntry *i = first_sub
			while i.next
				i = i.next
			i.next = e
		else
			first_sub = e
	void _show()
		printi(type)
		prints(" ")
		printi(size)
		prints(" ")
		prints(name)
		prints("\n")
	
	void show()
		_show()
		FileEntry *e = first_sub
		while e
			prints("    ")
			e._show()
			e = e.next
	FileEntry *get_sub(cstring sname)
		FileEntry *e = first_sub
		while e
			if strcmp(&sname, &e.name) == 0
				return e
			e = e.next
		return nil

class FileSystemData
	FileEntry list[256] // TODO: dynamic memory
	int list_size
	FileEntry *root
	
	char buf[2048]
	int buf_used
	
	FileEntry *add(cstring name, int type, FileEntry *parent)
		FileEntry *e = &list[list_size]
		list_size ++
		strcpy(&e.name, &name)
		e.type = type
		e.next = nil
		e.first_sub = nil
		e.op = nil
		if parent
			parent.add(e)
		return e
	
	void Init()
		list_size = 0
		root = add("-root-", FILE_TYPE_DIR, nil)
	
	FileEntry *get(cstring path)
		if path[0] != '/'
			return nil
		if path[1] == 0
			return root
		FileEntry *e = root
		cstring sub
		int ppos = 1
		bool has_more = true
		while has_more
			strcpy(&sub, &path[ppos])
			cstring name
			for int i, 0, 256
				if (sub[i] == '/') or (sub[i] == 0)
					has_more = (sub[i] == '/')
					ppos += i + 1
					name[i] = 0
					break
				name[i] = sub[i]
			e = e.get_sub(name)
			if !e
				return nil
		return e
		
	void Read(int disk_no, int cluster_offset, char buffer[0]*)
		Ata.ReadSector(disk_no, cluster_offset * 4    , 1, &buffer[0])
		Ata.ReadSector(disk_no, cluster_offset * 4 + 1, 1, &buffer[512])
		Ata.ReadSector(disk_no, cluster_offset * 4 + 2, 1, &buffer[1024])
		Ata.ReadSector(disk_no, cluster_offset * 4 + 3, 1, &buffer[1536])
	
	int read_int()
		int *p = &buf[buf_used]
		buf_used += 4
		return *p
	void read_str(cstring s)
		int l = buf[buf_used] + buf[buf_used+1]*256
		buf_used += 2
		for int i, 0, l
			s[i] = buf[buf_used + i]
		buf_used += l
		s[l] = 0
		
	void Mount(PartitionData p, FileEntry *dir)
		prints("mounte...")
		Read(0, p.offset + 1, &buf)
		buf_used = 0
		int NumFiles = read_int()
		for int n, 0, NumFiles
			cstring name
			read_str(name)
			FileEntry *e = add(name, FILE_TYPE_FILE, dir)
			//e.type =
			read_int()
			e.size = read_int()
			read_int() // NumParts = 1
			e.offset = read_int()
			e.num_clusters = read_int()
		prints(" ok\n")

FileSystemData FileSystem

//--------------------------------------------------------------
// Gerätedateien

FileOperations dev_zero_ops
void dev_zero_open(FileEntry *f)
	nil
void dev_zero_close(FileEntry *f)
	nil
int dev_zero_read(FileEntry *f, cstring *buf, int size)
	for int i, 0, size
		buf[i] = 0
	return size
int dev_zero_write(FileEntry *f, cstring *buf, int size)
	return 0

FileOperations dev_null_ops
void dev_null_open(FileEntry *f)
	nil
void dev_null_close(FileEntry *f)
	nil
int dev_null_read(FileEntry *f, cstring *buf, int size)
	return 0
int dev_null_write(FileEntry *f, cstring *buf, int size)
	return size

FileOperations dev_rand_ops
int dev_rand_state
void dev_rand_open(FileEntry *f)
	nil
void dev_rand_close(FileEntry *f)
	nil
int dev_rand_read(FileEntry *f, cstring *buf, int size)
	for int i, 0, size
		dev_rand_state = (dev_rand_state * 23434 + 82374) % 8623
		buf[i] = dev_rand_state
	return size
int dev_rand_write(FileEntry *f, cstring *buf, int size)
	return 0


FileOperations dev_key_ops
cstring dev_key_buf
int dev_key_num
void dev_key_open(FileEntry *f)
	nil
void dev_key_close(FileEntry *f)
	nil
int dev_key_read(FileEntry *f, cstring *buf, int size)
	if size > dev_key_num
		size = dev_key_num
	for int i, 0, size
		buf[i] = dev_key_buf[i]
		dev_key_num --
		memcpy(&dev_key_buf, &dev_key_buf[1], dev_key_num)
	return size
int dev_key_write(FileEntry *f, cstring *buf, int size)
	return 0

FileOperations dev_tty_ops
void dev_tty_open(FileEntry *f)
	nil
void dev_tty_close(FileEntry *f)
	nil
int dev_tty_read(FileEntry *f, cstring *buf, int size)
	return 0
int dev_tty_write(FileEntry *f, cstring *buf, int size)
	prints_low(buf, size)
	return size

void InitDrivers()
	FileEntry *dev = FileSystem.add("dev", FILE_TYPE_DIR, FileSystem.root)
	
	FileEntry *dev_zero = FileSystem.add("zero", FILE_TYPE_CHAR, dev)
	dev_zero_ops._open = &dev_zero_open
	dev_zero_ops._close = &dev_zero_close
	dev_zero_ops._read = &dev_zero_read
	dev_zero_ops._write = &dev_zero_write
	dev_zero.op = &dev_zero_ops
	
	FileEntry *dev_null = FileSystem.add("null", FILE_TYPE_CHAR, dev)
	dev_null_ops._open = &dev_null_open
	dev_null_ops._close = &dev_null_close
	dev_null_ops._read = &dev_null_read
	dev_null_ops._write = &dev_null_write
	dev_null.op = &dev_null_ops
	
	dev_rand_state = 13
	
	FileEntry *dev_rand = FileSystem.add("random", FILE_TYPE_CHAR, dev)
	dev_rand_ops._open = &dev_rand_open
	dev_rand_ops._close = &dev_rand_close
	dev_rand_ops._read = &dev_rand_read
	dev_rand_ops._write = &dev_rand_write
	dev_rand.op = &dev_rand_ops
	
	dev_key_num = 0
	
	FileEntry *dev_key = FileSystem.add("keyboard", FILE_TYPE_CHAR, dev)
	dev_key_ops._open = &dev_key_open
	dev_key_ops._close = &dev_key_close
	dev_key_ops._read = &dev_key_read
	dev_key_ops._write = &dev_key_write
	dev_key.op = &dev_key_ops
	
	FileEntry *dev_tty = FileSystem.add("tty", FILE_TYPE_CHAR, dev)
	dev_tty_ops._open = &dev_tty_open
	dev_tty_ops._close = &dev_tty_close
	dev_tty_ops._read = &dev_tty_read
	dev_tty_ops._write = &dev_tty_write
	dev_tty.op = &dev_tty_ops
	


//--------------------------------------------------------------
// Dateizugriff

class OpenFileData
	bool used
	FileEntry *entry
	int pos_in_cluster
	int cluster_no
	char buffer[0]*

OpenFileData OpenFile[128]
int num_open_files

int get_handle()
	for int i, 0, num_open_files
		if !OpenFile[i].used
			OpenFile[i].used = true
			return i
	OpenFile[num_open_files].used = true
	num_open_files ++
	return num_open_files - 1
			

int sys_open(TaskData *caller, cstring filename)
	int index = get_handle()
	OpenFileData *of = &OpenFile[index]
	of.entry = caller.get_file(filename)
	if !of.entry
		of.used = false
		return -1
	of.cluster_no = 0
	of.pos_in_cluster = 0
	if of.entry.type == FILE_TYPE_FILE
		of.buffer = malloc(2048)
		int p_offset = Partition[CurrentPartition].offset
		FileSystem.Read(0, p_offset + of.entry.offset, of.buffer)
	else if of.entry.type == FILE_TYPE_CHAR
		__temp_p__ = of.entry.op._open
		__temp_p2__ = of.entry
		asm{
			mov eax, __temp_p__
			push __temp_p2__
			call eax
			sub esp, 0x04
		}
	return index

int sys_read(TaskData *caller, int handle, char buffer[0]*, int size)
	if (handle < 0) or (handle >= num_open_files)
		return -1
	int r=0

	OpenFileData *of = &OpenFile[handle]
	if !of.used
		return -1
		
	if of.entry.type == FILE_TYPE_CHAR
		__temp_p__ = of.entry.op._read
		__temp_p2__ = of.entry
		__temp_p3__ = buffer
		__temp_i__ = size
		asm{
			mov eax, __temp_p__
			push __temp_i__
			push __temp_p3__
			push __temp_p2__
			call eax
			mov __temp_i__, eax
			sub esp, 0x12
		}
		return __temp_i__
		
	if size > of.entry.size - of.cluster_no * 2048 - of.pos_in_cluster
		size = of.entry.size - of.cluster_no * 2048 - of.pos_in_cluster
		
	while size > 0
	
		// wieviel kann aus dem Puffer gelesen werden?
		int cur_size = size
		if cur_size > 2048 - of.pos_in_cluster
			cur_size = 2048 - of.pos_in_cluster

		// direkt lesen (aus dem Lese-Puffer kopieren)
		memcpy(buffer, &of.buffer[of.pos_in_cluster], cur_size)
		buffer = &buffer[cur_size]
		of.pos_in_cluster += cur_size
		r += cur_size
		size -= cur_size
		
		if of.pos_in_cluster >= 2048
			// nächsten Cluster einlesen
			of.pos_in_cluster = 0
			of.cluster_no ++
			int cl = of.entry.offset + of.cluster_no
			int p_offset = Partition[CurrentPartition].offset
			FileSystem.Read(0, p_offset + cl, of.buffer)
		
	return r


int sys_write(TaskData *caller, int handle, char buffer[0]*, int size)
	if (handle < 0) or (handle >= num_open_files)
		return -1
	int r=0

	OpenFileData *of = &OpenFile[handle]
	if !of.used
		return -1
		
	if of.entry.type == FILE_TYPE_CHAR
		__temp_p__ = of.entry.op._write
		__temp_p2__ = of.entry
		__temp_p3__ = buffer
		__temp_i__ = size
		asm{
			mov eax, __temp_p__
			push __temp_i__
			push __temp_p3__
			push __temp_p2__
			call eax
			mov __temp_i__, eax
			sub esp, 0x12
		}
		return __temp_i__
	return 0

class dirent_struct
	int struct_size
	int size
	int type
	cstring name

int sys_getdents(TaskData *caller, int handle, char buffer[0]*, int num)
	if (handle < 0) or (handle >= num_open_files)
		return -1

	OpenFileData *of = &OpenFile[handle]
	if !of.used
		return -1

	if of.entry.type != FILE_TYPE_DIR
		return -2
	
	FileEntry *e = of.entry.first_sub
	for int i, 0, of.pos_in_cluster
		e = e.next
		if !e
			return 0
	dirent_struct *r = buffer
	
	if !e
		return 0
	r.struct_size = strlen(&e.name) + 13
	r.size = e.size
	r.type = e.type
	strcpy(&r.name, &e.name)
	of.pos_in_cluster ++
	return 1

void sys_close(TaskData *caller, int handle)
	if (handle < 0) or (handle >= num_open_files)
		return
	OpenFileData *of = &OpenFile[handle]
	if !of.used
		return
	if of.entry.type == FILE_TYPE_FILE
		free(of.buffer)
	else if of.entry.type == FILE_TYPE_CHAR
		__temp_p__ = of.entry.op._close
		__temp_p2__ = of.entry
		asm{
			mov eax, __temp_p__
			push __temp_p2__
			call eax
			sub esp, 0x04
		}
	of.used = false
	of.entry = nil

//--------------------------------------------------------------
// Tasks

const int MAX_TASKS = 256
const int TASK_STACK_SIZE = 40960
const int NUM_PRE_GDT_ENTRIES = 5
const int TASK_SWITCH_DT = 20 // ms
const int KERNEL_PAGE_DIRECTORY = 0x00200000

enum
	TASK_STATUS_UNUSED
	TASK_STATUS_SLEEPING
	TASK_STATUS_RUNNING
	TASK_STATUS_WAITING
	TASK_STATUS_ZOMBIE
	//TASK_STATUS_DEAD

class TaskStateSegment
	int prev
	void *esp0
	int ss0
	void *esp1
	int ss1
	void *esp2
	int ss2
	int cr3 // =PDPR
	void *eip
	int eflags // Iterrupt Flag
	int eax
	int ecx
	int edx
	int ebx
	void *esp
	void *ebp
	int esi
	int edi
	int es, cs, ss, ds, fs, gs
	int x, y
	
	
	void set(void *stack_pos, int _cs, int _ds, int pvl, int page_dir, void *task)
		int _ss = _ds
		prev = 0 // previous TSS (dynamisch)
		esp0 = stack_pos
		ss0 = _ss * 8
		esp1 = stack_pos
		ss1 = _ss * 8
		esp2 = stack_pos
		ss2 = _ss * 8
		cr3 = page_dir
		eip = task
		eflags = 0x200 + pvl * 0x1000 // IterruptFlag + PVL
		eax = 0
		ecx = 0
		edx = 0
		ebx = 0
		esp = stack_pos
		ebp = stack_pos
		esi = 0
		edi = 0
		es = _ds * 8
		cs = _cs * 8
		ss = _ss * 8
		ds = _ds * 8
		fs = _ds * 8
		gs = _ds * 8
		x = 0
		y = 0

int TaskCallData[2]

enum
	WAITING_MODE_TASK_EXIT
	WAITING_MODE_FILE_READ

class TaskData
	TaskStateSegment tss
	int stack[0]*
	int desc // Index in GDT
	int pid, status
	int return_status
	int *wait_target
	int waiting_mode, waiting_param
	TaskData *parent
	cstring name
	cstring arg
	cstring cur_dir
	int mem_offset
	bool kernel_mode
	void start()
		status = TASK_STATUS_RUNNING
	
	// nur um einmalig den Scheduler zu starten
	void run_forced()
		/*prints("springe in ")
		printi(t.pid)
		prints(t.name)*/
		// "Task Gate Descriptor" ---- falsch, kein gate!
		TaskCallData[0] = 0
		TaskCallData[1] = 8 * desc
		//printi(t.desc)
		//prints(":\n")
		__temp_p__ = &TaskCallData[0]

		// in Task springen
		//t.status = TASK_STATUS_RUNNING
		asm{
			mov eax, __temp_p__
			//db 0xff
			//db 0x28
			//jmp_far [eax]
			db 0xff
			db 0x18
			//call_far [eax]
			//jmp_far 0x004000000000 //0x0040:0x00000000
		}
	int addr2kernel(int addr)
		if addr < 0x00800000
			return addr
		return mem_offset + addr - 0x00800000
	int addr2task(int addr)
		if addr < 0x00800000
			return addr
		return addr - mem_offset + 0x00800000
	
	int set_cur_dir(cstring *dir)
		FileEntry *e = get_file(*dir)
		if !e
			return -1
		if e.type != FILE_TYPE_DIR
			return -2
		if (*dir)[0] == '/'
			strcpy(&cur_dir, dir)
		else
			strcat(&cur_dir, dir)
		if cur_dir[strlen(&cur_dir) - 1] != '/'
			strcat(&cur_dir, &"/")
		return 0
		
	FileEntry *get_file(cstring path)
		if path[0] == '/'
			return FileSystem.get(path)
		cstring long
		strcpy(&long, &cur_dir)
		strcat(&long, &path)
		return FileSystem.get(long)
	void make_zombie()
		status = TASK_STATUS_ZOMBIE
		if !kernel_mode
			free_user_mem(mem_offset)
	void clean_up_deep()
		status = TASK_STATUS_UNUSED
		if !kernel_mode
			free_user_mem(mem_offset)
	
TaskData Task[MAX_TASKS]
TaskData *KernelTask
TaskData *IdleTask
TaskData *TimerTask
TaskData *SysCallTask
int CurPid
int RunningPid
int UserMemMask
bool InterruptsEnabled
bool SchedulerEnabled

class GlobalDescriptorData
	int a, b
	void set_base(int base, int limit, bool gran)
		a = (base << 16) + (limit & 0xffff) // base[15..0] limit[15..0]
		b = (base & 0xff000000) + (limit & 0x0f0000) + ((base >> 16) & 0xff)  // base[31..24],..limit[19..16] not busy, base[23..16]
		b += 0x00008000 // present
		if gran
			b += 0x00800000
	void set_data(int base, int limit, bool executable, int pvl)
		set_base(base, limit >> 12, true)
		b += 0x00401200
		if executable
			b += 0x00000800
		b += 0x00002000 * pvl
	void set_task(TaskData *t)
		set_base(_p2i(&t.tss), 103, false)
		b += 0x00000900 // not busy
	void set_busy(bool busy)
		if busy
			b = b | 0x200
		else
			b = b & 0xfffffdff

GlobalDescriptorData GlobalDescriptorTable[NUM_PRE_GDT_ENTRIES + MAX_TASKS]

void CreateUserPaging(int mem_offset, int dir_pos)
	// 8 mb kernel + 8 mb user
	int p[0]* = _i2p(dir_pos)
	for int i, 0, 1024
		p[i] = 0
	for int i, 0, 4
		p[i] = dir_pos + 0x00001000 * (i + 1) + 7
	// kernel pages (1:1)
	for int n, 0, 2
		p = _i2p(dir_pos + 0x00001000 * (n + 1))
		for int i, 0, 1024
			p[i] = 0x00001000 * i + 7 + n * 0x00400000
	// user pages
	for int n, 0, 2
		p = _i2p(dir_pos + 0x00001000 * (n + 3))
		for int i, 0, 1024
			p[i] = mem_offset + 0x00001000 * i + 7 + n * 0x00400000

int get_user_mem()
	for int i, 0, MAX_TASKS
		int mask = 1 << i
		if (UserMemMask & mask) == 0
			UserMemMask = UserMemMask | mask
			//prints("u")
			//printi(i)
			return (i + 1) * 0x00800000
	return 0

void free_user_mem(int p)
	int n = (p / 0x00800000) - 1
	//prints("fu")
	//printi(n)
	int mask = 0xffffffff - (1 << n)
	UserMemMask = UserMemMask & mask

int get_next_pid()
	for int i, CurPid, MAX_TASKS
		if Task[i].status == TASK_STATUS_UNUSED
			CurPid = i + 1
			return i
	for int i, 0, CurPid
		if Task[i].status == TASK_STATUS_UNUSED
			CurPid = i + 1
			return i
	return -1

TaskData *AddTask(cstring name, void *func, bool kernel_mode, TaskData *parent)
	int pid = get_next_pid()
	TaskData *t = &Task[pid]
	t.pid = pid

	strcpy(&t.name, &name)
	t.desc = NUM_PRE_GDT_ENTRIES + pid
	t.status = TASK_STATUS_SLEEPING
	if parent
		strcpy(&t.cur_dir, &parent.cur_dir)
	else
		strcpy(&t.cur_dir, &"/")
	t.parent = parent
	t.kernel_mode = kernel_mode
	if kernel_mode
		t.mem_offset = 0
		t.stack = malloc(TASK_STACK_SIZE)
		t.tss.set(t.stack, 1, 2, 0, KERNEL_PAGE_DIRECTORY, func)
	else
		t.stack = _i2p(0x00c00000)
		t.mem_offset = get_user_mem()
		int dir_pos = t.mem_offset + 0x10000
		CreateUserPaging(t.mem_offset, dir_pos)
		t.tss.set(t.stack, 3, 4, /*3*/ 0, dir_pos, func)
	GlobalDescriptorTable[t.desc].set_task(t)

	return t

// Scheduler
int GetNextTask(int pid)
	for int i, pid + 1, MAX_TASKS
		if Task[i].status == TASK_STATUS_RUNNING
			return i
	for int i, 0, pid+1
		if Task[i].status == TASK_STATUS_RUNNING
			return i
	return IdleTask.pid

void idle_func()
	while true
		//prints("-idle-")
		asm{
			hlt
		}

void InitTasks()
	prints("init Tasks")
	InterruptsEnabled = false
	SchedulerEnabled = false
	
	// null descriptor
	for int i, 0, NUM_PRE_GDT_ENTRIES + MAX_TASKS
		GlobalDescriptorTable[i].a = 0x00000000
		GlobalDescriptorTable[i].b = 0x00000000
	// kernel code
	GlobalDescriptorTable[1].set_data(0, 0xffffffff, true, 0)
	// kernel data
	GlobalDescriptorTable[2].set_data(0, 0xffffffff, false, 0)
	// user code
	GlobalDescriptorTable[3].set_data(0, 0xffffffff, true, 0)//3)
	// user data
	GlobalDescriptorTable[4].set_data(0, 0xffffffff, false, 0)//3)

	for int i, 0, MAX_TASKS
		Task[i].status = TASK_STATUS_UNUSED

	CurPid = 0
	UserMemMask = 0
	RunningPid = 0

	KernelTask = AddTask("kernel", nil, true, nil)
	IdleTask = AddTask("idle", &idle_func, true, KernelTask)
	
	// GDT laden
	__temp_i__ = sizeof(GlobalDescriptorTable) - 1
	__temp_p__ = &GlobalDescriptorTable
	asm{
		jmp _after_gdt_data
		_gdtr:
		dw 0x0000
		_gdtr_base:
		dd 0x00000000
		_after_gdt_data:
		mov eax, __temp_i__
		mov [_gdtr], eax
		mov eax, __temp_p__
		mov [_gdtr_base], eax
		lgdt [_gdtr]
	}

	//prints("\n  setze Kernel-Task-Register")
	__temp_i__ = KernelTask.desc * 8
	asm{
		mov eax, __temp_i__ // Descriptor[n]
		ltr ax
	}
	// markiert das aktuelle TSS als "busy"!

	prints("   ok\n")

/*int sys_getpid()
	asm{
		xor, eax, eax
		str ax
		mov __temp_i__, eax
	}
	int pid = (__temp_i__ >> 3) - NUM_PRE_GDT_ENTRIES*/

			

int sys_execute(TaskData *caller, cstring filename)
	int h = sys_open(caller, filename)
	if h < 0
		return -1
	TaskData *t = AddTask(filename, _i2p(0x00a00000), false, caller)
	/*prints("<<")
	printi(caller.pid)
	prints(" -> ")
	printi(t.pid)
	prints(">>")*/
	memcpy(&t.arg, &filename, sizeof(cstring))
	cstring *p = _i2p(t.mem_offset + 0x00200000)
	while true
		int r = sys_read(caller, h, p, 1024)
		if r <= 0
			break
		p = &p[r]
	sys_close(caller, h)
	
	t.start()
	return t.pid

void sys_exit(TaskData *caller, int r)
	caller.return_status = r
	caller.make_zombie()
	for int i, 0, MAX_TASKS
		if Task[i].status == TASK_STATUS_WAITING
			if Task[i].waiting_mode == WAITING_MODE_TASK_EXIT and Task[i].waiting_param == caller.pid
				Task[i].tss.eax = 0// // return waitpid()
				*Task[i].wait_target = r
				caller.clean_up_deep()
				Task[i].start()

int sys_waitpid(TaskData *caller, int pid, int *status, int options)
	if pid < 0 or pid >= MAX_TASKS
		return -1
	TaskData *t = &Task[pid]
	if t.status == TASK_STATUS_UNUSED
		return -1
	if t.status == TASK_STATUS_ZOMBIE
		*status = t.return_status
		t.clean_up_deep()
		caller.start()
		return 0
	if (options & 1) > 0
		return -2
	caller.wait_target = status
	caller.status = TASK_STATUS_WAITING
	caller.waiting_mode = WAITING_MODE_TASK_EXIT
	caller.waiting_param = pid
	return 0

int sys_kill(TaskData *caller, int pid, int signal)
	if pid < 0 or pid >= MAX_TASKS
		return -1
	TaskData *t = &Task[pid]
	if t.status == TASK_STATUS_UNUSED
		return -1
	if signal == 9
		t.clean_up_deep()
	else if signal == 19
		t.status = TASK_STATUS_SLEEPING
	else if signal == 18
		t.status = TASK_STATUS_RUNNING
	return 0


void panic(cstring s)
	asm{
		cli
	}
	TextColor=0x04
	prints("\n\n                   --  Panik  --\n\n")
	prints(s)
	while true
		nil

//--------------------------------------------------------------
// Interrupts


void int_handler(int i)
	if !Task[RunningPid].kernel_mode
		sys_exit(&Task[RunningPid], i-666)
		asm{
			sti
		}
		while true
			nil
	cstring s, nn
	strcpy(&s, &"Int ")
	_i2s(i, &nn)
	strcat(&s, &nn)
	if i==0
		strcat(&s, &": Division durch Null")
	if i==1
		strcat(&s, &": Debug")
	if i==3
		strcat(&s, &": Breakpoint")
	if i==4
		strcat(&s, &": Overflow")
	if i==5
		strcat(&s, &": Bounds Check")
	if i==6
		strcat(&s, &": Invalider Opcode")
	if i==7
		strcat(&s, &": kein Ko-Prozessor")
	if i==8
		strcat(&s, &": System-Fehler")
	if i==9
		strcat(&s, &": Ko-Prozessor-Segment overrun")
	if i==10
		strcat(&s, &": Invalides TSS")
	if i==11
		strcat(&s, &": Segment fehlt")
	if i==12
		strcat(&s, &": Stack-Fehler")
	if i==13
		strcat(&s, &": General Protection Fault")
	if i==14
		strcat(&s, &": Page-Fault")
	if i==16
		strcat(&s, &": Koprozessor-Fehler")
	strcat(&s, &"  8-P")
	panic(s)

void int_00()
	int_handler(0)
void int_01()
	int_handler(1)
void int_02()
	int_handler(2)
void int_03()
	int_handler(3)
void int_04()
	int_handler(4)
void int_05()
	int_handler(5)
void int_06()
	int_handler(6)
void int_07()
	int_handler(7)
void int_08()
	int_handler(8)
void int_09()
	int_handler(9)
void int_10()
	int_handler(10)
void int_11()
	int_handler(11)
void int_12()
	int_handler(12)
void int_13()
	int_handler(13)
void int_14()
	int_handler(14)
void int_15()
	int_handler(15)
void int_16()
	int_handler(16)
void int_XX()
	int_handler(2004)

class InterruptDescriptor
	int a, b

	void set(void *func)
		if !func
			a = 0
			b = 0
		else
			int addr = _p2i(func)
			// Interrupt Gate
			a = 0x00080000 + (addr & 0xffff) // desc[1],addr[15..0]
			b = (addr & 0xffff0000) + 0x00008e00 // addr[31..16],typ

	TaskData *set_task(cstring name, void *func)
		if !func
			a = 0
			b = 0
		else
			TaskData *t = AddTask(name, func, true, nil)
			t.tss.eflags = 0x000 // Iterrupt Flag = 0

			// Interrupt Task Gate
			a = 0x00080000*t.desc // desc,0[15..0]
			b = 0x00008500 // 0[31..16],typ
			return t
		return nil

InterruptDescriptor InterruptDescriptorTable[256]

void int_key()
	while true
		outportb(0x20, 0x20)
		int k = inportb(0x60)
		/*prints("<IRQ ")
		printi(k)
		prints(">")*/
		if dev_key_num < 64
			dev_key_buf[dev_key_num] = k
			dev_key_num ++
		asm{
			iret
		}

void int_timer()
	while true
		if SchedulerEnabled
		
			int pid_cur = (TimerTask.tss.prev >> 3) - NUM_PRE_GDT_ENTRIES
			int pid_next = GetNextTask(pid_cur)
			GlobalDescriptorTable[Task[pid_cur].desc].set_busy(false)
			GlobalDescriptorTable[Task[pid_next].desc].set_busy(true)
			TimerTask.tss.prev = (pid_next + NUM_PRE_GDT_ENTRIES) << 3
			RunningPid = pid_next
	/*		if next != last
				prints("<")
				prints(Task[pid_cur].name)
				prints(",")
				prints(Task[pid_next].name)
				//printi(pid_next)
				prints(">")*/

		// weitere Aufrufe erlauben
		outportb(0x20,0x20)

		// zurück
		asm{
			iret
		}

void show_pvl()
	asm{
		db 0x9c // pushf
		pop eax
		mov __temp_i__, eax
	}
	int eflags = __temp_i__
	int pvl = (eflags >> 12) & 3
	prints("[PVL:")
	printi(pvl)
	prints("]")

void sys_showstats()
	prints("------------------------------\nMem: ")
	printh(&UserMemMask, 2)
	TextPos = 16
	printi(MemAlloc[NumMemAllocs-1].End - MemAlloc[0].Offset)
	prints("\n")
	for int i, 0, MAX_TASKS
		if Task[i].status != TASK_STATUS_UNUSED
			printi(i)
			TextPos = 3
			prints(Task[i].name)
			TextPos = 20
			if Task[i].kernel_mode
				prints("K")
			else
				prints("U")
			TextPos = 23
			if Task[i].status == TASK_STATUS_RUNNING
				prints("R")
			else if Task[i].status == TASK_STATUS_SLEEPING
				prints("S")
			else if Task[i].status == TASK_STATUS_WAITING
				prints("W")
			else if Task[i].status == TASK_STATUS_ZOMBIE
				prints("Z")
			else
				printi(Task[i].status)
			prints("\n")

	
void int_syscall()
	asm{
		cli
	}
	while true
		//show_pvl()
		int r = 0
		
		int pid = (SysCallTask.tss.prev >> 3) - NUM_PRE_GDT_ENTRIES
		TaskData *caller = &Task[pid]
		//printi(pid)
		int index = caller.tss.eax
		int param1 = caller.tss.ebx
		int param2 = caller.tss.ecx
		int param3 = caller.tss.edx
	
		if index == 1 // exit
			sys_exit(caller, param1)
		else if index == 3 // read
			cstring *p = _i2p(caller.addr2kernel(param2))
			r = sys_read(caller, param1, p, param3)
		else if index == 4 // write
			p = _i2p(caller.addr2kernel(param2))
			r = sys_write(caller, param1, p, param3)
		else if index == 5 // open
			p = _i2p(caller.addr2kernel(param1))
			r = sys_open(caller, *p)
		else if index == 6 // close
			sys_close(caller, param1)
		else if index == 7 // waitpid
			p = _i2p(caller.addr2kernel(param2))
			r = sys_waitpid(caller, param1, p, param3)
		else if index == 11 // exec
			p = _i2p(caller.addr2kernel(param1))
			r = sys_execute(caller, *p)
		else if index == 12 // chdir
			p = _i2p(caller.addr2kernel(param1))
			r = caller.set_cur_dir(p)
		else if index == 20 // getpid
			r = pid
		else if index == 37 // kill
			r = sys_kill(caller, param1, param2)
		else if index == 141 // getdents
			p = _i2p(caller.addr2kernel(param2))
			r = sys_getdents(caller, param1, p, param3)
		else if index == 183 // getcwd
			p = _i2p(caller.addr2kernel(param1))
			strcpy(p, &caller.cur_dir)
		else if index == 0x2001 // getarg
			r = caller.addr2task(_p2i(&caller.arg))
		else if index == 0x2002 // prints
			p = _i2p(caller.addr2kernel(param1))
		else if index == 0x2003 // showstats
			sys_showstats()
		else
			prints("<<")
			printi(index)
			prints(">>")
		/*TextColor = 4
		prints("<<")
		prints(caller.name)
		prints(":")
		printi(index)
		prints(">>")
		TextColor = 7*/
		//while true
		//	nil
		
		// task switch?
		if caller.status != TASK_STATUS_RUNNING
			GlobalDescriptorTable[caller.desc].set_busy(false)
			int pid_next = GetNextTask(pid)
			/*prints("<<")
			printi(pid_next)
			prints(">>")*/
			GlobalDescriptorTable[Task[pid_next].desc].set_busy(true)
			SysCallTask.tss.prev = (pid_next + NUM_PRE_GDT_ENTRIES) << 3
			RunningPid = pid_next

		// zurück
		caller.tss.eax = r
		if InterruptsEnabled
			asm{
				sti
			}
		asm{
			iret
			cli
		}

void InitInterrupts()
	prints("lade Interrupt-Tabelle")

	for int i, 0, 256
		InterruptDescriptorTable[i].set(nil)

	InterruptDescriptorTable[ 0].set(&int_00)
	InterruptDescriptorTable[ 1].set(&int_01)
	InterruptDescriptorTable[ 2].set(&int_02)
	InterruptDescriptorTable[ 3].set(&int_03)
	InterruptDescriptorTable[ 4].set(&int_04)
	InterruptDescriptorTable[ 5].set(&int_05)
	InterruptDescriptorTable[ 6].set(&int_06)
	InterruptDescriptorTable[ 7].set(&int_07)
	InterruptDescriptorTable[ 8].set(&int_08)
	InterruptDescriptorTable[ 9].set(&int_09)
	InterruptDescriptorTable[10].set(&int_10)
	InterruptDescriptorTable[11].set(&int_11)
	InterruptDescriptorTable[12].set(&int_12)
	InterruptDescriptorTable[13].set(&int_13)
	InterruptDescriptorTable[14].set(&int_14)
	InterruptDescriptorTable[15].set(&int_15)
	InterruptDescriptorTable[16].set(&int_16)
	TimerTask = InterruptDescriptorTable[32].set_task("timer", &int_timer)
	InterruptDescriptorTable[33].set_task("key", &int_key)
	SysCallTask = InterruptDescriptorTable[0x80].set_task("syscall", &int_syscall)
	//InterruptDescriptorTable[0x80].set(&int_syscall)

	// IRQs auf höhere Interrupts legen
	outportb(0x20,0x11)
	outportb(0xa0,0x11)
	outportb(0x21,0x20)
	outportb(0xa1,0x28)
	outportb(0x21,0x04)
	outportb(0xa1,0x02)
	outportb(0x21,0x01)
	outportb(0xa1,0x01)
	outportb(0x21,0x00)
	outportb(0xa1,0x00)
	
	__temp_p__ = &InterruptDescriptorTable

	asm{
		jmp _after_idt_data
		_idtr:
		dw 0x07ff
		_idtr_base:
		dd 0x00000000
		_after_idt_data:
		mov eax, __temp_p__
		mov [_idtr_base], eax
		cli
		lidt [_idtr]
		sti
	}

	/*outportb(0x70,0x08)
	char c=inportb(0x71)
	outportb(0x71,c|0x40)*/

	/*prints("versuche boese Speicher-Operation...")

	int i = 0
	1/i
	asm{
		mov [0x00000000], eax
	}*/
	
	InterruptsEnabled = true
	prints("   ok\n")

void InitTimer()
	// ticks/s: 1193180
	// 100Hz:   11932 = 0x2e9c
	int ticks_per_sec = 1193180
	int ticks_till_irq = (ticks_per_sec >> 10) * TASK_SWITCH_DT
	if ticks_till_irq > 0xffff
		ticks_till_irq = 0xffff
	//1/0
	outportb(0x43, 0x34)
	outportb(0x40, ticks_till_irq) //0x9c)
	outportb(0x40, ticks_till_irq >> 8) //0x2e)

void StartScheduler()
	prints("starte Scheduler\n")
	InitTimer()
	SchedulerEnabled = true
	TimerTask.run_forced()

void LoadKaLib()
	int h = sys_open(KernelTask, "/kalib")
	if h < 0
		panic("kann KaLib-Datei nicht lesen!")
		return
	cstring *p = _i2p(0x00030000)
	while true
		int r = sys_read(KernelTask, h, p, 1024)
		if r <= 0
			break
		p = &p[r]
	sys_close(KernelTask, h)

/*void FileTest()
	int h = sys_open("/test.txt")
	if h < 0
		prints("## kann Datei nicht lesen! ##\n")
		return
	cstring s
	int r = sys_read(h, &s, 256)
	s[r] = 0
	prints(s)
	sys_close(h)

void DriverTest()
	prints("Treiber Test\n")
	int h = sys_open("/dev/keyboard")
	if h < 0
		prints("## kann Datei nicht lesen! ##\n")
		return
	while true
		cstring s
		int r = sys_read(h, &s, 64)
		if r > 0
			printh(&s, r)
			prints(" ")
	sys_close(h)*/


void InitPaging()
	prints("Paging")
	// 8 mb flat paging
	int p[0]* = _i2p(KERNEL_PAGE_DIRECTORY)
	for int i, 0, 1024
		p[i] = KERNEL_PAGE_DIRECTORY + 0x00001000 * (i + 1) + 7
	for int n, 0, 16
		p = _i2p(KERNEL_PAGE_DIRECTORY + 0x00001000 * (n + 1))
		for int i, 0, 1024
			p[i] = 0x00001000 * i + 7 + n * 0x00400000
		
	// :P
	//p[1023] = 0x00012007
	
	__temp_i__ = KERNEL_PAGE_DIRECTORY
	asm{
		mov eax, __temp_i__
		mov cr3, eax
		mov eax, cr0
		or eax, 0x80000000
		mov cr0, eax
		
		// flush cpu prefetch
		jmp _flush
		_flush:
	}
	prints(" ok\n")

void PagingTest()
	printh(_i2p(0x007ff000), 256)


void main()
	read_data_from_init()
	etm_init()
	NumMemAllocs=0
	
	prints("Willkommen zu Eusebius 0.2.4   (c) by MichiSoft TM 2014\n")
	prints("Speicher: ")
	printi(MemorySize)
	prints("k\n")
	
	Ata.Init()
	ReadPartitions(0)
	ShowPartitions()
	
	FileSystem.Init()
	FileSystem.Mount(Partition[0], FileSystem.root)
	/*FileEntry *e = FileSystem.get("/dev/b")
	if e
		e.show()
	FileSystem.root.show()*/
	
	InitPaging()
	
	InitTasks()
	
	InitInterrupts()
	
	InitDrivers()
	
	//PagingTest()

	//FileTest()
	//DriverTest()
	
	LoadKaLib()
	
	if sys_execute(KernelTask, "/shell") < 0
		panic("kann /shell-Datei nicht lesen!")
	//sys_execute(KernelTask, "/hello")
	//sys_execute(KernelTask, "/hello")
	
	StartScheduler()
	
	panic("keine Tasks")
