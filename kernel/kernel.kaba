#define __OS__
use "base.kaba"
use "text_mode.kaba"
use "ata.kaba"
use "filesystem.kaba"
use "std_char_devs.kaba"
use "mem/heap.kaba"
use "mem/paging.kaba"
use "mem/shared.kaba"
use "file_access.kaba"
use "task/tasks.kaba"
use "task/scheduler.kaba"
use "interrupts.kaba"
use "syscall.kaba"
#define __STRING_CONST_AS_CSTRING__

const int KALIB_LOCATION = 0x00030000

//#disasm

void show_pvl()
	asm{
		db 0x9c // pushf
		pop eax
		mov __temp_i__, eax
	}
	int eflags = __temp_i__
	int pvl = (eflags >> 12) & 3
	prints("[PVL:")
	printi(pvl)
	prints("]")

void LoadKaLib()
	if VerbosityLevel > 0
		prints("loading kalib... ")
	int h = KernelTask.sys_open("/lib/kalib", O_RDONLY)
	printi(h)
	if h < 0
		panic("kann /lib/kalib nicht lesen!")
		return
	int addr = KALIB_LOCATION
	while true
		int r = KernelTask.sys_read(h, addr, 1024)
		if r <= 0
			break
		addr += r
	KernelTask.sys_close(h)
	if VerbosityLevel > 0
		prints("ok\n")

/*void FileTest()
	int h = sys_open("/test.txt")
	if h < 0
		prints("## kann Datei nicht lesen! ##\n")
		return
	cstring s
	int r = sys_read(h, &s, 256)
	s[r] = 0
	prints(s)
	sys_close(h)

void DriverTest()
	prints("Treiber Test\n")
	int h = sys_open("/dev/keyboard")
	if h < 0
		prints("## kann Datei nicht lesen! ##\n")
		return
	while true
		cstring s
		int r = sys_read(h, &s, 64)
		if r > 0
			printh(&s, r)
			prints(" ")
	sys_close(h)*/


void PagingTest()
	printh(_i2p(0x007ff000), 256)


void main()
	read_data_from_init()
	mem_init()
	etm_init()
	SchedulerEnabled = false
	
	prints("Willkommen zu Eusebius 0.2.11   (c) by MichiSoft TM 2015\n")
	prints("Speicher: ")
	printi(MemorySize / 1024)
	prints("k\n")
	
	Paging.init()
	
	SharedMem.init()
	
	Ata.init()
	ReadPartitions(0)
	ShowPartitions()
	
	InitFileAccessData()
	
	FileSystem.init()
	
	FileSystem.mount(&Partition[2], FileSystem.root, "ext2")
	FileEntry *boot = FileSystem.get("/boot", FileSystem.root)
	FileSystem.mount(&Partition[1], boot, "michifs")
	
	InitTasks()
	
	Interrupts.init()
	
	InitDrivers()
	
	//PagingTest()

	//FileTest()
	//DriverTest()
	
	LoadKaLib()
	
	int stdin = KernelTask.sys_open("/dev/tty", O_RDONLY)
	int stdout = KernelTask.sys_open("/dev/tty", O_WRONLY)
	
	if KernelTask.sys_execute("/bin/shell", stdin, stdout) < 0
		panic("kann /bin/shell-Datei nicht lesen!")
	
	StartScheduler()
	
	panic("keine Tasks")

