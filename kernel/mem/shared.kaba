use common
use io.text
use mem.heap
use mem.paging

const MAX_SHARED_MEMORY_BLOCKS = 256


class SharedMemory

	class Block as @noauto
		var id: int
		var key: int
		var pid: int
		var size: int
		var p: int
		var refcount: int
		var flags: int
	
		func mut unref()
			refcount --
		
			if refcount == 0 and (flags & SHM_DEST) > 0
				clean_up()
			if refcount < 0
				panic("SharedMem.refcount < 0")
	
		func mut clean_up()
			Paging.free_chunk(p, size)
			key = -1
	var static blocks: Block[MAX_SHARED_MEMORY_BLOCKS]
	
	func static init()
		for i=>b in blocks
			b.id = i
			b.key = -1
	
	func static get_used() -> int
		var size = 0
		for b in blocks
			if b.key >= 0
				size += b.size
		return size
	
	func static create(key: int, size: int, flags: int, pid: int) -> int
		let absolute = (flags & SHM_ABSOLUTE) != 0
		if key < 0 and !absolute
			return -EINVAL
		for i=>b in blocks
			if b.key != -1
				continue
			if absolute and key == 0x00700000
				# :P
				b.p = key
			else if absolute
				if !Paging.alloc_chunk(key, size, true)
					return -ENOMEM
				b.p = key
			else
				b.p = Paging.find_and_alloc_chunk(size)
				if b.p == -1
					return -ENOMEM
			b.size = ceil_quot(size, PAGE_SIZE) * PAGE_SIZE
			b.key = key
			b.pid = pid
			b.refcount = 0
			b.flags = flags
			return i
		return -ENOSPC
		
	func static get_by_key(key: int) -> Block*
		if key == -1
			return nil
		for i=>b in blocks
			if b.key == key
				# test access rights
				return &b
		return nil
		
	func static get(id: int) -> Block*
		if id < 0 or id >= MAX_SHARED_MEMORY_BLOCKS
			return nil
		if blocks[id].key == -1
			return nil
		return &blocks[id]
		
	func static shm_get(key: int, size: int, flags: int, pid: int) -> int
		let s = get_by_key(key)
		if s
			if (flags & IPC_CREAT) > 0 and (flags & IPC_EXCL) > 0
				return -EEXIST
			return s.id
		if (flags & IPC_CREAT) == 0
			return -ENOENT
		return create(key, size, flags, pid)

