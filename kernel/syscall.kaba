#define __OS__
use "base.kaba"
use "text_mode.kaba"
use "filesystem.kaba"
use "file_access.kaba"
use "base_mem.kaba"
use "tasks.kaba"
use "paging.kaba"
#define __STRING_CONST_AS_CSTRING__



void sys_showstats()
	prints("------------------------------\nMem:  ")
	printi(Paging.get_used() / 1024)
	prints("k von ")
	printi(MemorySize / 1024)
	prints("k       Heap:  ")
	printi((MemAlloc[NumMemAllocs-1].End - MemAlloc[0].Offset) / 1024)
	prints("k\n")
	for int i, 0, MAX_TASKS
		if Task[i].status != TASK_STATUS_UNUSED
			printi(i)
			TextPos = 3
			prints(Task[i].name)
			TextPos = 20
			if Task[i].kernel_mode
				prints("K")
			else
				prints("U")
			TextPos = 23
			if Task[i].status == TASK_STATUS_RUNNING
				prints("R")
			else if Task[i].status == TASK_STATUS_SLEEPING
				prints("S")
			else if Task[i].status == TASK_STATUS_WAITING
				prints("W")
			else if Task[i].status == TASK_STATUS_ZOMBIE
				prints("Z")
			else
				printi(Task[i].status)
			TextPos = 27
			printi(Task[i].page_dir.count_pages())
			prints("\n")

	
void int_syscall()
	asm{
		cli
	}
	while true
		//show_pvl()
		int r = 0
		
		int pid = (SysCallTask.tss.prev >> 3) - NUM_PRE_GDT_ENTRIES
		TaskData *caller = &Task[pid]
		//printi(pid)
		int index = caller.tss.eax
		int param1 = caller.tss.ebx
		int param2 = caller.tss.ecx
		int param3 = caller.tss.edx
	
		if index == 1 // exit
			caller.sys_exit(param1)
		else if index == 3 // read
			r = caller.sys_read(param1, param2, param3)
		else if index == 4 // write
			r = caller.sys_write(param1, param2, param3)
		else if index == 5 // open
			cstring filename
			caller.page_dir.memcpy2kernel(&filename, param1, sizeof(cstring))
			r = caller.sys_open(filename)
		else if index == 6 // close
			caller.sys_close(param1)
		else if index == 7 // waitpid
			r = caller.sys_waitpid(param1, param2, param3)
		else if index == 11 // exec
			caller.page_dir.memcpy2kernel(&filename, param1, sizeof(cstring))
			r = caller.sys_execute(filename)
		else if index == 12 // chdir
			caller.page_dir.memcpy2kernel(&filename, param1, sizeof(cstring))
			r = caller.set_cur_dir(&filename)
		else if index == 20 // getpid
			r = pid
		else if index == 37 // kill
			r = caller.sys_kill(param1, param2)
		else if index == 45 // brk
			r = caller.sys_brk(param1)
		else if index == 141 // getdents
			r = caller.sys_getdents(param1, param2, param3)
		else if index == 183 // getcwd
			caller.page_dir.memcpy2task(param1, &caller.cur_dir, sizeof(cstring))
		else if index == 0x2001 // getarg
			caller.page_dir.memcpy2task(param1, &caller.arg, sizeof(cstring))
			nil//r = caller.addr2task(_p2i(&caller.arg))
		else if index == 0x2002 // prints
			nil//p = _i2p(caller.addr2kernel(param1))
		else if index == 0x2003 // showstats
			sys_showstats()
		else
			prints("<<")
			printi(index)
			prints(">>")
		/*TextColor = 4
		prints("<<")
		prints(caller.name)
		prints(":")
		printi(index)
		prints(">>")
		TextColor = 7*/
		//while true
		//	nil
		
		// task switch?
		if caller.status != TASK_STATUS_RUNNING
			GlobalDescriptorTable[caller.desc].set_busy(false)
			int pid_next = GetNextTask(pid)
			/*prints("<<")
			printi(pid_next)
			prints(">>")*/
			GlobalDescriptorTable[Task[pid_next].desc].set_busy(true)
			SysCallTask.tss.prev = (pid_next + NUM_PRE_GDT_ENTRIES) << 3
			RunningPid = pid_next

		// zurÃ¼ck
		caller.tss.eax = r
		if InterruptsEnabled
			asm{
				sti
			}
		asm{
			iret
			cli
		}
