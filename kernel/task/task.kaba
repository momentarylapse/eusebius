use "../base.kaba"
use "../io/text.kaba"
use "../fs/fs.kaba"
use "../fs/pipe.kaba"
use "../fs/access.kaba"
use "../mem/heap.kaba"
use "../mem/paging.kaba"
use "../mem/shared.kaba"
use "../net/socket.kaba"
use "../time/timevalue.kaba"




const int MAX_OPEN_FILES_PER_TASK = 16
const int MAX_SHARED_MEMS_PER_TASK = 16

const int SIGKILL = 9
const int SIGSEGV = 11
const int SIGCONT = 18
const int SIGSTOP = 19
const int SIGX = 23


//--------------------------------------------------------------
// Tasks

const int KERNEL_STACK_SIZE = 0x00020000 // 128k
const int KERNEL_MEM = 0x00800000 // 8mb
const int IDLE_STACK_SIZE = 0x00001000 // 4k

const int MAX_TASKS = 256
const int TASK_IMAGE_POS = KERNEL_MEM
const int TASK_STACK_SIZE = 0x00080000 // 512k
const int TASK_STACK_POS = 0x00880000
const int NUM_PRE_GDT_ENTRIES = 5
const int INITIAL_TASK_MEM = 0x00800000 // 8m

const int TASK_MAX_MESSAGES = 16

enum
	TASK_STATUS_UNUSED
	TASK_STATUS_SLEEPING
	TASK_STATUS_RUNNING
	TASK_STATUS_WAITING
	TASK_STATUS_ZOMBIE
	TASK_STATUS_INTERRUPT
	//TASK_STATUS_DEAD

class TaskStateSegment
	int prev
	void *esp0
	int ss0
	void *esp1
	int ss1
	void *esp2
	int ss2
	PageDirectory *cr3 // =PDPR
	void *eip
	int eflags // Iterrupt Flag
	int eax
	int ecx
	int edx
	int ebx
	void *esp
	void *ebp
	int esi
	int edi
	int es, cs, ss, ds, fs, gs
	int x, y
	
	
	void set(void *stack_pointer, int _cs, int _ds, int pvl, PageDirectory *page_dir, void *task)
		int _ss = _ds
		prev = 0 // previous TSS (dynamisch)
		esp0 = stack_pointer
		ss0 = _ss * 8
		esp1 = stack_pointer
		ss1 = _ss * 8
		esp2 = stack_pointer
		ss2 = _ss * 8
		cr3 = page_dir
		eip = task
		eflags = 0x200 + pvl * 0x1000 // IterruptFlag + PVL
		eax = 0
		ecx = 0
		edx = 0
		ebx = 0
		esp = stack_pointer
		ebp = stack_pointer
		esi = 0
		edi = 0
		es = _ds * 8
		cs = _cs * 8
		ss = _ss * 8
		ds = _ds * 8
		fs = _ds * 8
		gs = _ds * 8
		x = 0
		y = 0

int[2] TaskCallData

enum
	WAITING_MODE_TASK_EXIT
	WAITING_MODE_FILE_READ
	WAITING_MODE_FILE_WRITE
	WAITING_MODE_TIME
	WAITING_MODE_MESSAGE
	WAITING_MODE_SOCKET_ACCEPT

class TaskSharedMemoryRef
	SharedMemory *s
	int dest // task space

int __handler_p__

void TaskSignalHandler()//void *p)
	asm{
		//mov __handler_p__, eax
	}
	//__temp_p__ = p
	//prints("-----signal-handler-----\n")
	//printh(&__handler_p__, 4)
	//__temp_p__ = p
	asm{
		mov eax, __handler_p__
		call eax
	}
	//prints("[fertig...return]\n")
	asm{	
		// return from handler
		mov eax, 0x00002006
		int 0x80
	}
	while true
		nil
	

class Task
	TaskStateSegment tss
	int[0]* stack
	int stack_size
	int desc // Index in GDT
	int pid, puid, status
	int return_status
	int waiting_mode
	int waiting_param_i1, waiting_param_i2, waiting_param_i3
	bool sleep_and_wait
	Task* parent
	cstring name
	cstring arg
	FileEntry* cur_dir
	PageDirectory *page_dir
	int mem_bottom, mem_size
	
	TimeValue time_all, time_temp
	int load // 1/1000
	
	bool kernel_mode
	OpenFileData*[MAX_OPEN_FILES_PER_TASK] handles
	TaskSharedMemoryRef[MAX_SHARED_MEMS_PER_TASK] shmem
	
	int[32] signal_handlers
	TaskStateSegment signal_reg_buf
	bool in_signal
	
	void init(int _pid, cstring _name, Task *_parent)
		for s in signal_handlers
			s = 0
		sleep_and_wait = false
			
		pid = _pid
		puid = NextPuidToAssign
		NextPuidToAssign ++

		strcpy(&name, &_name)
		desc = NUM_PRE_GDT_ENTRIES + pid
		status = TASK_STATUS_SLEEPING
		parent = _parent
		if parent
			cur_dir = parent.cur_dir
		else
			cur_dir = FileSystem.root
		kernel_mode = false
	
		/*t.stack_size = TASK_STACK_SIZE
	t.stack = _i2p(TASK_STACK_POS)
	t.mem_bottom = KERNEL_MEM
	t.mem_size = INITIAL_TASK_MEM
	t.page_dir = paging.create_page_directory()
	t.page_dir.map_lin(PAGE_SIZE, PAGE_SIZE, KERNEL_MEM - PAGE_SIZE, false)
	if !t.page_dir.map_by_alloc(t.mem_bottom, t.mem_size, true)
		panic("incomplete page dir") // ...
	t.tss.set(t.get_initial_stack_pos(), 3, 4, 3, t.page_dir, func)
	GlobalDescriptorTable[t.desc].set_task(t)*/
	
		time_all.clear()
		load = 0
		time_temp.clear()
	
	void __assign__(Task o)
		nil
	
	void activate()
		if sleep_and_wait
			status = TASK_STATUS_SLEEPING
			sleep_and_wait = false
		else
			status = TASK_STATUS_RUNNING
	
	// nur um einmalig den Scheduler zu starten
	void run_forced()
		/*prints("springe in ")
		printi(t.pid)
		prints(t.name)*/
		// "Task Gate Descriptor" ---- falsch, kein gate!
		TaskCallData[0] = 0
		TaskCallData[1] = 8 * desc
		//printi(t.desc)
		//prints(":\n")
		__temp_p__ = &TaskCallData[0]

		// in Task springen
		//t.status = TASK_STATUS_RUNNING
		asm{
			mov eax, __temp_p__
			//db 0xff
			//db 0x28
			//jmp_far [eax]
			db 0xff
			db 0x18
			//call_far [eax]
			//jmp_far 0x004000000000 //0x0040:0x00000000
		}
	int addr2kernel(int addr, int* offset)
		return page_dir.addr2kernel(addr, offset)
	
	int set_cur_dir(cstring dir)
		FileEntry *e = get_file(dir)
		if !e
			return -1
		if e.type != FILE_TYPE_DIR
			return -2
		cur_dir = e
		return 0
		
	FileEntry *get_file(cstring path)
		return FileSystem.get(path, cur_dir)
		
	int add_handle(OpenFileData *oh)
		for h,i in handles
			if !h
				h = oh
				oh.ref_count ++
				return i
		return -1

	void clean_up_resources()
		for h in handles
			if h
				h.unref()
				h = nil
		
		for s in shmem
			if s.s
				page_dir.unmap(s.dest, s.s.size, false)
				s.s.unref()
				s.s = nil
				
		if !kernel_mode
			if page_dir
				page_dir.unmap(0, KERNEL_MEM, false)
				page_dir.unmap(mem_bottom, mem_size, true)
				paging.free_page(_p2i(page_dir))
				page_dir = nil

	void clean_up_deep()
		clean_up_resources()
		status = TASK_STATUS_UNUSED
		puid = -1
		
	
	void send_death_message()
		for t in Tasks
			if t.status == TASK_STATUS_WAITING
				if t.waiting_mode == WAITING_MODE_TASK_EXIT and t.waiting_param_i1 == pid
					t.tss.eax = 0// // return waitpid()
					t.page_dir.memcpy2task(t.waiting_param_i2, &return_status, 4)
					clean_up_deep()
					t.activate()
		
	void *get_initial_stack_pos()
		return _i2p(_p2i(stack) + stack_size)
	
	void set_wait(int mode, int param_i1, int param_i2, int param_i3)
		status = TASK_STATUS_WAITING
		waiting_mode = mode
		waiting_param_i1 = param_i1
		waiting_param_i2 = param_i2
		waiting_param_i3 = param_i3
	
	int pages_alloced()
		int n = mem_size / PAGE_SIZE
		n += page_dir.count_entries() // all page tables
		n ++ // page_dir itself
		return n
	
	
	void die(int r)
		return_status = r
		clean_up_resources()
		status = TASK_STATUS_ZOMBIE
		send_death_message()
		
	int get_prev_pid()
		return (tss.prev >> 3) - NUM_PRE_GDT_ENTRIES
		
	int handle_signal(int signal)
		if signal < 0 or signal >= 32
			return -EINVAL
		
		if in_signal
			die(0) // we're very friendly :-P


		if signal_handlers[signal] != 0
			in_signal = true
			memcpy(&signal_reg_buf, &tss, sizeof(TaskStateSegment))
			tss.eax = signal_handlers[signal]
			__handler_p__ = tss.eax
			tss.esp = _i2p(_p2i(tss.esp) + 1024)
			tss.ebp = tss.esp
			tss.eip = &TaskSignalHandler
		else
			// default actions
			if signal == SIGSTOP
				if status == TASK_STATUS_RUNNING
					status = TASK_STATUS_SLEEPING
				else if status == TASK_STATUS_WAITING
					sleep_and_wait = true
			else if signal == SIGCONT
				if status == TASK_STATUS_SLEEPING
					status = TASK_STATUS_RUNNING
				else if status == TASK_STATUS_WAITING
					sleep_and_wait = false
			else
				die(0)
		return 0

//--------------------------------------------------------------
// Dateizugriff
	
	OpenFileData *get_handle(int handle)
		if (handle < 0) or (handle >= MAX_OPEN_FILES_PER_TASK)
			return nil
		return handles[handle]

	int sys_open(int faddr, int flags)
		cstring filename
		if !page_dir.memcpy2kernel(&filename, faddr, sizeof(cstring))
			return -EFAULT
		OpenFileData *of = kernel_open(filename, flags, cur_dir)
		if !of
			return -ENOENT
		int index = add_handle(of)
		if index < 0
			of.unref()
		return index


	int sys_read(int handle, int addr, int size)
		OpenFileData *of = get_handle(handle)
		if !of
			return -EBADF
		int r = of.read(page_dir, addr, size)
		if r == -EWOULDBLOCK and of.blocking
			set_wait(WAITING_MODE_FILE_READ, handle, addr, size)
		return r


	int sys_write(int handle, int addr, int size)
		OpenFileData *of = get_handle(handle)
		if !of
			return -EBADF
		int r = of.write(page_dir, addr, size)
		if r == -EWOULDBLOCK and of.blocking
			set_wait(WAITING_MODE_FILE_WRITE, handle, addr, size)
		return r

	int sys_getdents(int handle, int addr, int num)
		OpenFileData *of = get_handle(handle)
		if !of
			return -EBADF
		return of.getdents(page_dir, addr, num)


	int sys_seek(int handle, int offset, int mode)
		OpenFileData *of = get_handle(handle)
		if !of
			return -EBADF
		return of.seek(offset, mode)

	int sys_close(int handle)
		OpenFileData *of = get_handle(handle)
		if !of
			return -EBADF
		of.unref()
		handles[handle] = nil
		return 0
	
	int sys_dup(int fd)
		OpenFileData *of = get_handle(fd)
		if !of
			return -EBADF
		int index = add_handle(of)
		return index
	
	int sys_pipe(int addr)
		FileEntry *e = create_file_entry(":pipe:", FILE_TYPE_PIPE, nil)
		if !e
			return -ENFILE
		if !create_pipe(e)
			return -3
		OpenFileData *of_in = kernel_open_entry(e, O_RDONLY)
		OpenFileData *of_out = kernel_open_entry(e, O_WRONLY)
		int[2] fd
		fd[0] = add_handle(of_in)
		fd[1] = add_handle(of_out)
		if fd[0] < 0 or fd[1] < 0
			return -2
		if !page_dir.memcpy2task(addr, &fd, 8)
			return -EFAULT
		return 0
	
	int sys_set_cur_dir(int faddr)
		cstring filename
		if !page_dir.memcpy2kernel(&filename, faddr, sizeof(cstring))
			return -EFAULT
		return set_cur_dir(filename)
		
	int sys_mkdir(int faddr)
		cstring filename
		if !page_dir.memcpy2kernel(&filename, faddr, sizeof(cstring))
			return -EFAULT
		return kernel_mkdir(filename, cur_dir)

	int sys_mknod(int faddr, int flags)
		cstring filename
		if !page_dir.memcpy2kernel(&filename, faddr, sizeof(cstring))
			return -EFAULT
		return kernel_mknod(filename, flags, cur_dir)
		
	int sys_unlink(int faddr)
		cstring filename
		if !page_dir.memcpy2kernel(&filename, faddr, sizeof(cstring))
			return -EFAULT
		return kernel_unlink(filename, cur_dir)
		
	int sys_rmdir(int faddr)
		cstring filename
		if !page_dir.memcpy2kernel(&filename, faddr, sizeof(cstring))
			return -EFAULT
		return kernel_rmdir(filename, cur_dir)
	
	int sys_execute(int faddr, int fd_in, int fd_out)
		int h = sys_open(faddr, O_RDONLY)
		if h < 0
			return -ENOENT
		OpenFileData *of = handles[h]
		cstring filename
		of.entry.get_path(filename)
		int addr = TASK_IMAGE_POS // task space...
		Task* t = AddTaskUser(filename, _i2p(addr), self)
		if !t
			sys_close(h)
			return -ENOMEM
		if !page_dir.memcpy2kernel(&t.arg, faddr, sizeof(cstring))
			return -EFAULT
		while true
			//int r = sys_read(caller, h, addr, 1024)
			int r = of.read(t.page_dir, addr, 1024)
			if r <= 0
				break
			addr += r
		sys_close(h)
		
		OpenFileData *of_in = get_handle(fd_in)
		OpenFileData *of_out = get_handle(fd_out)
		t.add_handle(of_in)
		t.add_handle(of_out)
	
		t.activate()
		return t.pid

	void sys_exit(int r)
		die(r)

	int sys_waitpid(int _pid, int status_addr, int options)
		Task* t = get_task(_pid)
		if !t
			return -ECHILD
		if t.status == TASK_STATUS_ZOMBIE
			page_dir.memcpy2task(status_addr, &t.return_status, 4)
			t.clean_up_deep()
			activate()
			return 0
		if (options & 1) > 0
			return -2
		set_wait(WAITING_MODE_TASK_EXIT, _pid, status_addr, 0)
		return 0

	int sys_kill(int _pid, int signal)
		Task* t = get_task(_pid)
		if !t
			return -ESRCH
		return t.handle_signal(signal)
	
	int sys_nanosleep(int p1, int p2)
		TimeValue tv
		if !page_dir.memcpy2kernel(&tv, p1, sizeof(tv))
			return -EFAULT
		set_wait(WAITING_MODE_TIME, tv.sec, tv.nanosec, 0)
		return 0

	int sys_brk(int new_top)
		new_top = ceil_quot(new_top, PAGE_SIZE) * PAGE_SIZE
		int cur_top = mem_bottom + mem_size
		if new_top <= cur_top
			return cur_top
		if !page_dir.map_by_alloc(cur_top, new_top - cur_top, true)
			return -ENOMEM
		mem_size = new_top - mem_bottom
		return new_top
	
	int sys_fcntl(int fd, int op, int param)
		OpenFileData *of = get_handle(fd)
		if !of
			return -EBADF
		if op == F_GETFL
			return of.get_flags()
		if op == F_SETFL
			of.set_flags(param)
			return 0
		return -EINVAL
	
	int sys_fork()
		Task* t = AddTaskUser("-fork-", _i2p(TASK_IMAGE_POS), self)
		if !t
			return -ENOMEM
		
		// copy "normal" memory
		t.sys_brk(mem_bottom + mem_size)
		page_dir.copy_pages_to(t.page_dir, mem_bottom, mem_bottom, mem_size)
		
		// link shared memory
		for r,i in shmem
			if !r.s
				t.shmem[i].s = r.s
				t.shmem[i].dest = r.dest
				r.s.refcount ++
				t.page_dir.map_lin(r.dest, r.s.p, r.s.size, true)
		
		// open files...
		// TODO
		
		// data
		memcpy(&t.arg, &arg, sizeof(cstring))
		strcpy(&t.name, &name)
		strcpy(&t.cur_dir, &cur_dir)
		t.parent = self
		t.tss = tss
		t.tss.cr3 = t.page_dir
		t.tss.eax = 0 // fork() return for child
	
		t.activate()
		return t.pid
	
	int sys_ipc(int cmd, int p1, int p2, int p3)
		if cmd == SHMGET
			return SharedMem.shm_get(p1, p2, p3, pid)
		else if cmd == SHMAT
			for r in shmem
				if !r.s
					r.s = SharedMem.get(p1)
					if !r.s
						return -ENOENT
					r.dest = page_dir.find_free_space(0xe0000000, r.s.size) //0xa0000000
					if r.dest == -1
						return -ENOMEM
					r.s.refcount ++
					page_dir.map_lin(r.dest, r.s.p, r.s.size, true)
					if !page_dir.memcpy2task(p2, &r.dest, 4)
						return -EFAULT
					return 0
			return -1
		else if cmd == SHMDT
			for r in shmem
				if r.s and r.dest == p1
					page_dir.unmap(r.dest, r.s.size, false)
					r.s.unref()
					r.s = nil
					return 0
			return -1
		else if cmd == 1001 // wait-for-message
			set_wait(WAITING_MODE_MESSAGE, 0, 0, 0)
			return 0
		else if cmd == 1002 // yield / wake-up
			Task*t = get_task(p1)
			if t
				t.activate()
			set_wait(WAITING_MODE_TIME, 0, 0, 0)
			return 0
		return -EINVAL
	
	int sys_socket(int cmd, int p1, int p2)
		if cmd == SYS_SOCKET
			FileEntry *e = create_file_entry(":sock:", FILE_TYPE_SOCKET, nil)
			if !e
				return -ENFILE
			if !create_socket(e, p1)
				return -3
			OpenFileData *of = kernel_open_entry(e, O_RDONLY)
			return add_handle(of)
		else if cmd == SYS_BIND
			OpenFileData *of = get_handle(p1)
			if !of
				return -EBADF
			if of.entry.type != FILE_TYPE_SOCKET
				return -ENOTSOCK
			return of.socket.bind(p2)
		else if cmd == SYS_CONNECT
			OpenFileData *of = get_handle(p1)
			if !of
				return -EBADF
			if of.entry.type != FILE_TYPE_SOCKET
				return -ENOTSOCK
			return of.socket.connect(p2)
		else if cmd == SYS_LISTEN
			OpenFileData *of = get_handle(p1)
			if !of
				return -EBADF
			if of.entry.type != FILE_TYPE_SOCKET
				return -ENOTSOCK
			return of.socket.listen()
		else if cmd == SYS_ACCEPT
			OpenFileData *of = get_handle(p1)
			if !of
				return -EBADF
			if of.entry.type != FILE_TYPE_SOCKET
				return -ENOTSOCK
			int e = of.socket.accept_start()
			if e < 0
				return e
			set_wait(WAITING_MODE_SOCKET_ACCEPT, p1, 0, 0)
			return 0
		return -EINVAL
	
	int sys_sigaction(int sig, int addr)
		if sig < 0 or sig >= 32 or sig == SIGSTOP or sig == SIGKILL
			return -EINVAL
		signal_handlers[sig] = addr
		return 0
		
	int sys_exit_signal()
		if !in_signal
			return -1
		
		in_signal = false
		memcpy(&tss, &signal_reg_buf, sizeof(TaskStateSegment))
		return 0

Task *get_task(int pid)
	if pid < 0 or pid >= MAX_TASKS
		return nil
	Task* t = &Tasks[pid]
	if t.status == TASK_STATUS_UNUSED
		return nil
	return t

Task[MAX_TASKS] Tasks
Task* KernelTask
Task* IdleTask
int NextPidToAssign
int NextPuidToAssign
int RunningPid

class GlobalDescriptorData
	int a, b
	void set_base(int base, int limit, bool gran)
		a = (base << 16) + (limit & 0xffff) // base[15..0] limit[15..0]
		b = (base & 0xff000000) + (limit & 0x0f0000) + ((base >> 16) & 0xff)  // base[31..24],..limit[19..16] not busy, base[23..16]
		b += 0x00008000 // present
		if gran
			b += 0x00800000
	void set_data(int base, int limit, bool executable, int pvl)
		set_base(base, limit >> 12, true)
		b += 0x00401200
		if executable
			b += 0x00000800
		b += 0x00002000 * pvl
	void set_task(Task *t)
		set_base(_p2i(&t.tss), 103, false)
		b += 0x00000900 // not busy
	void set_busy(bool busy)
		if busy
			b = b | 0x200
		else
			b = b & 0xfffffdff

GlobalDescriptorData[NUM_PRE_GDT_ENTRIES + MAX_TASKS] GlobalDescriptorTable

int get_next_pid()
	for i in NextPidToAssign:MAX_TASKS
		if Tasks[i].status == TASK_STATUS_UNUSED
			NextPidToAssign = i + 1
			return i
	for i in 0:NextPidToAssign
		if Tasks[i].status == TASK_STATUS_UNUSED
			NextPidToAssign = i + 1
			return i
	return -1

Task* AddTaskUser(cstring name, void *func, Task *parent)
	int pid = get_next_pid()
	if pid < 0
		return nil
	Task* t = &Tasks[pid]
	
	t.init(pid, name, parent)
	t.kernel_mode = false
	
	t.stack_size = TASK_STACK_SIZE
	t.stack = _i2p(TASK_STACK_POS)
	t.mem_bottom = KERNEL_MEM
	t.mem_size = INITIAL_TASK_MEM
	t.page_dir = paging.create_page_directory()
	t.page_dir.map_lin(PAGE_SIZE, PAGE_SIZE, KERNEL_MEM - PAGE_SIZE, false)
	if !t.page_dir.map_by_alloc(t.mem_bottom, t.mem_size, true)
		panic("incomplete page dir") // ...
	t.tss.set(t.get_initial_stack_pos(), 3, 4, 3, t.page_dir, func)
	GlobalDescriptorTable[t.desc].set_task(t)

	return t

Task* AddTaskKernel(cstring name, void *func, int stack_size, Task* parent)
	int pid = get_next_pid()
	if pid < 0
		return nil
	Task* t = &Tasks[pid]

	t.init(pid, name, parent)
	t.kernel_mode = true
	
	t.stack_size = stack_size
	t.stack = malloc(stack_size)
	t.page_dir = KernelPageDir
	t.mem_bottom = 0
	t.mem_size = 0
	t.tss.set(t.get_initial_stack_pos(), 1, 2, 0, KernelPageDir, func)
	
	GlobalDescriptorTable[t.desc].set_task(t)

	return t

void idle_func()
	while true
		//prints("<idle>")
		asm{
			hlt
		}

void InitTasks()
	prints("init Tasks...")
	
	// null descriptor
	for g in GlobalDescriptorTable
		g.a = 0x00000000
		g.b = 0x00000000
	// kernel code
	GlobalDescriptorTable[1].set_data(0, 0xffffffff, true, 0)
	// kernel data
	GlobalDescriptorTable[2].set_data(0, 0xffffffff, false, 0)
	// user code
	GlobalDescriptorTable[3].set_data(0, 0xffffffff, true, 0)//3)
	// user data
	GlobalDescriptorTable[4].set_data(0, 0xffffffff, false, 0)//3)

	for t in Tasks
		t.status = TASK_STATUS_UNUSED
		for h in t.handles
			h = nil
		for sm in t.shmem
			sm.s = nil

	NextPidToAssign = 0
	NextPuidToAssign = 0
	//UserMemMask = 0
	RunningPid = 0

	KernelTask = AddTaskKernel("kernel", nil, KERNEL_STACK_SIZE, nil)
	IdleTask = AddTaskKernel("idle", &idle_func, IDLE_STACK_SIZE, KernelTask)
	
	// GDT laden
	__temp_i__ = sizeof(GlobalDescriptorTable) - 1
	__temp_p__ = &GlobalDescriptorTable
	asm{
		jmp _after_gdt_data
		_gdtr:
		dw 0x0000
		_gdtr_base:
		dd 0x00000000
		_after_gdt_data:
		mov eax, __temp_i__
		mov [_gdtr], eax
		mov eax, __temp_p__
		mov [_gdtr_base], eax
		lgdt [_gdtr]
	}

	//prints("\n  setze Kernel-Task-Register")
	__temp_i__ = KernelTask.desc * 8
	asm{
		mov eax, __temp_i__ // Descriptor[n]
		ltr ax
	}
	// markiert das aktuelle TSS als "busy"!
	
	// enable sse
	asm{
	
		mov eax, cr0
		and eax, 0xfffffffb // clear CR0.EM (don't emulate via IRQ)
		or eax, 0x00000002 // set CR0.MP (inform about task-switches)
		mov cr0, eax
		mov __temp_i__, eax
		
		// set CR4.OSFXSR (enable sse)
		mov eax, cr4
		//mov __temp_i__, eax
		or eax, 0x00000200
		mov cr4, eax
	
		// flush cpu prefetch
		jmp _flush_sse
		_flush_sse:
	}

	prints("  ok\n")

/*int sys_getpid()
	asm{
		xor, eax, eax
		str ax
		mov __temp_i__, eax
	}
	int pid = (__temp_i__ >> 3) - NUM_PRE_GDT_ENTRIES*/

			

