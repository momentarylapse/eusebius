string hex(char[0]* p, int size)
	string s
	for i in 0:size
		s.add(p[i])
	return s.hex(false)
string i2h(int i)
	return hex(&i, 4)

class BEUShort
	char[2] c
	int int()
		int a = c[0]
		int b = c[1]
		return (a << 8) + b
class BESShort
	char[2] c
	int int()
		int a = c[0]
		int b = c[1]
		int r = (a << 8) + b
		if (r & 0x8000) != 0
			r -= 1<<16
		return r
class BELong
	char[4] x
	int int()
		int a = x[0]
		int b = x[1]
		int c = x[2]
		int d = x[3]
		return (a << 24) + (b << 16) + (c << 8) + d

class Fixed
	BEUShort a, b

class F214
	BESShort t
	float float()
		return t.int() / 16384.0

void read(File *f, char[0]* p, int size)
	string s = f.read(size)
	for i in 0:size
		p[i] = s[i]

string single_utf8(int code)
	string s
	if (code & 0xffffff80) == 0
		s.add(code)
	else if (code & 0xfffff800) == 0
		s.add(0xc0 + (code >> 6))
		s.add(0x80 + (code & 0x3f))
	return s

string utf32_to_utf8(int[] codes)
	string s
	for c in codes
		s += single_utf8(c)
	return s

class TTFTableDirectory
	Fixed version
	BEUShort num_tables
	BEUShort seach_range
	BEUShort entry_selector
	BEUShort range_shift

class TTFTableDirectoryEntry
	BELong tag, chksum, offset, length
	string name()
		string s
		s.add(tag.x[0])
		s.add(tag.x[1])
		s.add(tag.x[2])
		s.add(tag.x[3])
		return s

class TTFMapHeader
	BEUShort version, num_tables

class TTFMapTable
	int platform, encoding
	int offset
	int format
	
	# format 4
	int[] end_code
	int[] start_code
	int[] id_delta
	int[] id_range_offset
	
	# format 6
	int first_code
	int[] glyph_id_array
	
	int map(int code)
		if format == 4
			for i in 0:end_code.num
				if code >= start_code[i] and code <= end_code[i]
					return (code + id_delta[i]) & 0x0000ffff
			return -1
		if format == 6
			if code < first_code or code >= (first_code + glyph_id_array.num)
				return -1
			return glyph_id_array[code - first_code]
		return -1

class TTFHeader
	Fixed version
	Fixed revision
	BELong chksm
	BELong magic
	BEUShort flags
	BEUShort units_per_em
	# more


class TTFContour
	string flags
	complex[] p
	

class TTFGlyph
	int code
	int xmin, ymin, xmax, ymax
	TTFContour[] contours
	int[] codes
	float lsb, width

class TTF
	TTFTableDirectory td
	TTFTableDirectoryEntry[] tdentries
	TTFHeader head
	
	TTFMapHeader mh
	TTFMapTable[] map_tables
	TTFGlyph[] glyphs
	
	int[] lsb
	int[] advance
	
	File *f
	
	int readL()
		BELong t
		read(f, &t, 4)
		return t.int()

	int readUS()
		BEUShort t
		read(f, &t, 2)
		return t.int()

	int readSS()
		BESShort t
		read(f, &t, 2)
		return t.int()

	int readUB()
		int t = 0
		read(f, &t, 1)
		return t

	float readF214()
		F214 t
		read(f, &t, 2)
		return t.float()
	
	bool read_table_directory()
	
		read(f, &td, 12)
		int n = td.num_tables.int()
		if n > 1000
			return false
		for i in 0:n
			TTFTableDirectoryEntry e
			read(f, &e, 16)
			tdentries.add(e)
	
		for ee in tdentries
			print(ee.name() + " " + ee.offset.int())
		return true
	
	bool read_head()
		TTFTableDirectoryEntry *te = get_table("head")
		if !te
			print("no head table")
			return false
		f.seek(te.offset.int(), true)
		print("head-----------------------------")
		
		read(f, &head, 32)
		print(head.units_per_em.int())
		
		return true
	
	bool try_read_hhead()
		TTFTableDirectoryEntry *te = get_table("hhea")
		if !te
			return false
		f.seek(te.offset.int(), true)
		print("hhead-----------------------------")
		
		f.seek(34, false)
		int n = readUS()
		print(n)
		lsb.resize(n)
		advance.resize(n)
		
		return true
		
	bool try_read_hmetrix()
		TTFTableDirectoryEntry *te = get_table("hmtx")
		if !te
			return false
		f.seek(te.offset.int(), true)
		print("hmetrix-----------------------------")
		for i in 0:lsb.num
			advance[i] = readUS()
			lsb[i] = readSS()
		
		return true
	
	bool read_mapping()
		TTFTableDirectoryEntry *te = get_table("cmap")
		if !te
			print("no cmap table")
			return false
		f.seek(te.offset.int(), true)
		
		read(f, &mh, 4)
		int n = mh.num_tables.int()
		print("map-----------------------------")
		print(i2h(f.getPos()))
		#print mh.version.int()
		#print n
		if n > 100000
			print("too many mapping tables")
			return false
			
		for i in 0:n
			TTFMapTable t
			t.platform = readUS()
			t.encoding = readUS()
			t.offset = readL()
			map_tables.add(t)
		
		for m in map_tables
			print("--------------")
			print(m.offset)
			print(m.platform)
			print(m.encoding)
			f.seek(te.offset.int() + m.offset, true)
			m.format = readUS()
			
			if m.format == 0
				int length = readUS()
				print("l " + length)
				int version = readUS()
				for i in 0:256
					m.glyph_id_array.add(readUB())
				#print m.glyph_id_array
			else if m.format == 4
				int length = readUS()
				print("l " + length)
				int version = readUS()
				int seg_count = readUS() / 2
				print("s " + seg_count)
				int search_range = readUS()
				int entry_selector = readUS()
				int range_shift = readUS()
				for i in 0:seg_count
					m.end_code.add(readUS())
				readUS()
				for i in 0:seg_count
					m.start_code.add(readUS())
				for i in 0:seg_count
					m.id_delta.add(readUS())
				for i in 0:seg_count
					m.id_range_offset.add(readUS())
				int rs = seg_count * 8 + 16
				print("r: " + (length - rs))
				int na = (length - rs) / 2
				for i in 0:na
					m.glyph_id_array.add(readUS())
				
				#print start_count
#				print end_count
#				print id_delta
#				print id_range_offset
#				for i in 0:seg_count
#					print "--"
#					print start_count[i]
#					print end_count[i]
#					print id_delta[i]
#					print id_range_offset[i]
				
				#for i in 0:seg_count
#					if m.id_range_offset[i] != 0
#						print("id_range_offset != 0")
#						
#						print(m.id_range_offset)
#						return false
					
				#int length = readUS(f)
				#int length = readUS(f)
				
				#return false
			else if m.format == 6
				int length = readUS()
				print("l " + length)
				int version = readUS()
				m.first_code = readUS()
				int entry_count = readUS()
				for i in 0:entry_count
					m.glyph_id_array.add(readUS())
				#print m.glyph_id_array
			
			else if m.format == 12
				readUS() # "padding"
				int length = readL()
				print("l " + length)
				int version = readL()
				
				int group_count = readL() / 2
				print("s " + group_count)
				
				for i in 0:group_count
					int start_code = readL()
					m.start_code.add(start_code)
					m.end_code.add(readL())
					m.id_delta.add(readL() - start_code)
					
			else
				print("unhandled format: " + m.format)
				return false
				
				
		return true
	
	bool read_glyph(TTFGlyph g)
		print("glyph---------------------------------")
		print(i2h(f.getPos()))
		int num_contours = readSS()
		print(num_contours)
		if num_contours == 0
			#print("--empty")
			return true
		if num_contours > 200
			print("too many contours: " + num_contours)
			return false
		
		g.code = -1
		g.xmin = readSS()
		g.ymin = readSS()
		g.xmax = readSS()
		g.ymin = readSS()
		#print "" + g.xmin + ":" + g.xmax
		#print "" + g.ymin + ":" + g.ymax
		
		if num_contours < 0
			return read_glyph_compound(g)
		return read_glyph_simple(g, num_contours)
	
	bool read_glyph_compound(TTFGlyph g)
		#print("---compound")
		int n = 0
		int flags = 0
		
		while true
			#print "C"
			flags = readUS()
			int glyph_index = readUS()
			#print glyph_index
		
			if (flags & 0x0001) != 0
				int arg1 = readSS()
				int arg2 = readSS()
			else
				int arg1 = readUB()
				int arg2 = readUB()
		
			if (flags & 0x0008) != 0
				float scale = readF214()
			else if (flags & 0x0040) != 0
				float xscale = readF214()
				float yscale = readF214()
			else if (flags & 0x0080) != 0
				float xxscale = readF214()
				float xyscale = readF214()
				float yxscale = readF214()
				float yyscale = readF214()
			
			if (flags & 0x0020) == 0
				break
			n ++
			if n > 50
				print("too many sub glyphs: " + n)
				return false
		
		if (flags & 0x0100) != 0
			int instrlen = readUS()
			if instrlen > 10000
				print("too many instructions: " + instrlen)
				return false
			f.read(instrlen)
			
		return true
	
	bool read_glyph_simple(TTFGlyph g, int num_contours)
		#print("simple")
		
		# endpoints
		int[] endpoints
		for i in 0:num_contours
			endpoints.add(readUS())
		
		for e in endpoints
			#print "e " + e
			if e > 1000
				print("invalid endpoint: " + e)
				return false
		
		# instructions???
		int instrlen = readUS()
		if instrlen > 10000
			print("too many instructions: " + instrlen)
			return false
		#print "instr: " + instrlen
		f.read(instrlen) # instr
		
		if num_contours > 0
			int num_points = endpoints[endpoints.num-1] + 1
			#print "points: " + num_points
		
			# flags
			string flags = read_contour_flags(num_points)
			#print flags.hex(false)
		
			#print i2h(f.getPos())
			float[] x = read_contour_coords(num_points, flags, 0x02, 0x10)
			float[] y = read_contour_coords(num_points, flags, 0x04, 0x20)
			#print x
			#print y
			
			int i0 = 0
			for e,ci in endpoints
				TTFContour c
				int i1 = e + 1
				for i in i0:i1
					c.flags.add(flags[i])
					c.p.add(complex(x[i], y[i]))
				i0 = i1
				g.contours.add(c)
				c.__delete__() # TODO repair kaba :P
				c.__init__()
		
		#print i2h(f.getPos())
		
		# 4byte alignment
		int n = f.getPos()
		if (n & 3) != 0
			f.read(4 - (n & 3))
		return true
	
	string read_contour_flags(int n)
		string flags
		flags.resize(n)
		
		for i in 0:n
			#print i
			int c = readUB()
			flags[i] = c
			
			if (c & 0x08) != 0x00
				int cn = readUB()
				#print "repeat " + cn
				for j in 0:cn
					flags[i+j+1] = c
				i += cn
		
		return flags
	
	float[] read_contour_coords(int n, string flags, int mask_short, int mask_same)
		float[] x
		x.resize(n)
		float cur = 0
		for i in 0:n
			int ff = flags[i]
			
			bool is_same = (ff & mask_same) != 0
			bool is_short = (ff & mask_short) != 0
			if is_same and !is_short
				nil#print "rep"
			else
				int t = 0
				if is_short
					#print "1b"
					t = readUB()
					if !is_same
						t = -t
				else
					#print "2b"
					t = readSS()
				cur += t
			x[i] = cur
					
		return x
	
	bool read_glyphs()
		TTFTableDirectoryEntry *te = get_table("glyf")
		if !te
			print("no glyf table")
			return false
		f.seek(te.offset.int(), true)
		int _max = te.offset.int() + te.length.int() - 32
		
		TTFGlyph gg
		glyphs.add(gg)
		glyphs.add(gg)
		glyphs.add(gg)
		
		while f.getPos() < _max
			TTFGlyph g
			if !read_glyph(g)
				return false
			glyphs.add(g)
		print("Glyphs: " + glyphs.num)
		#for gg in glyphs
#			print "---- " + gg.contours.num
#			for c in gg.contours
#				print c.flags.hex(false)
#				string s = ""
#				for z in c.p
#					s += "" + z + ", "
#				print "[" + s + "]"
			
		
		return true
	
	bool load(string filename)

		f = FileOpen(filename)
		if !f
			return false
		f.setBinaryMode(true)
		
		if !read_table_directory()
			return false
		if !read_head()
			return false
		if !read_mapping()
			return false
		if try_read_hhead()
			try_read_hmetrix()
		if !read_glyphs()
			return false
			
		update_codes()
		update_size()
	
		delete f
		return true
		
	TTFTableDirectoryEntry* get_table(string tag)
		for ee in tdentries
			if tag == ee.name()
				return &ee
		return nil
	
	int map(int code)
		for m in map_tables
			int n = m.map(code)
			if n >= glyphs.num
				print("mapping error: " + code + " => " + n)
				return -1
			if n >= 0
				return n
		return -1
	
	TTFGlyph* get(int code)
		print("get " + code)
		int n = map(code)
		print(n)
		if n < 0
			return &glyphs[0]
		return &glyphs[n]
	
	void map_code(int index, int code)
		if index < glyphs.num
			glyphs[index].code = code
			bool contains = false
			for c in glyphs[index].codes
				if c == code
					contains = true
			if !contains
				glyphs[index].codes.add(code)
			print("E: " + index + " => " + code)
	
	void update_codes()
	
		for m in map_tables
			if m.format == 6
				print("----------------------- 666")
				for n,i in m.glyph_id_array
					map_code(n, m.first_code + i)
			else if m.format == 4
				print("----------------------- 444")
				print(m.end_code.num)
				for i in 0:m.end_code.num
					print("----")
					print(m.start_code[i])
					print(m.end_code[i])
					print(m.id_delta[i])
					for code in m.start_code[i]:(m.end_code[i]+1)
						int n
						if m.id_range_offset[i] == 0
							n = (code + m.id_delta[i]) & 0x0000ffff
						else
							int index = (code - m.start_code[i]) + m.id_range_offset[i]/2 - (m.id_range_offset.num - i)
							n = m.glyph_id_array[index]
						map_code(n, code)
			else if m.format == 12
				print("----------------------- 12 12 12")
				print(m.end_code.num)
				for i in 0:m.end_code.num
					print("----")
					print(m.start_code[i])
					print(m.end_code[i])
					print(m.id_delta[i])
					for code in m.start_code[i]:(m.end_code[i]+1)
						map_code(code + m.id_delta[i], code)
			else
				print("---------------- UNHANDLED " + m.format)
	
	void update_size()
		float scale = 1.0 / head.units_per_em.int()
		float w = 0
		for g,i in glyphs
			g.lsb = 0
			g.width = g.xmax - g.xmin
			if i < lsb.num
				g.lsb = lsb[i]
				g.width = advance[i]
				w = g.width
			else if lsb.num > 0
				g.width = w
			
	
	
	void export(string filename)
	
		File *f = FileCreate(filename)
		f.setBinaryMode(true)
		
		int num_glyphs = 0
		for g in glyphs
			num_glyphs += g.codes.num
		
		f << num_glyphs
		int num_p = 0
		float scale = 1.0 / head.units_per_em.int()
		for g in glyphs
			for code in g.codes
				f << code
				f << g.width * scale
				f << 0.0
				f << g.contours.num
				for c in g.contours
					f << num_p
					f << c.p.num
					num_p += c.p.num
		f << num_p
		int xx = 0
		complex[] pp
		for g in glyphs
			for code in g.codes
				for c in g.contours
					for p in c.p
						pp.add(p)
						f << (p.x - g.xmin + g.lsb) * scale
						f << p.y * scale
						xx ++
		for g in glyphs
			for c in g.contours
				if c.flags.num > 0
					if (c.flags[0] & 1) == 0
						print("-----[0] not on curve!")
				for x in c.flags
					f << ((x & 1) != 0)
		
		
		delete(f)

class FontWindow extends Window
	float scale
	int index
	void __init__()
		super.__init__("test", -1, -1, 500, 500)
		addDrawingArea("!grabfocus", 0, 0, 0, 0, "area")
		index = 0
		float font_size = 500.0
		scale = font_size / font.head.units_per_em.int()
	
	override void onKeyDown()
		if HuiGetEvent().key == KeyLeft
			index --
		if HuiGetEvent().key == KeyRight
			index ++
		index = loopi(index, 0, font.glyphs.num)
		redraw("area")
	
	float xx(float x)
		return 100 + x*scale
	
	float yy(float y)
		return 400 -y*scale
	
	override void onDraw(Painter *p)
		#Painter *p = beginDraw("area")
		float w = p.width
		float h = p.height
		TTFGlyph *g = &font.glyphs[index]
		
		p.setColor(White)
		p.drawRect(0, 0, w, h)
		
		p.setColor(color(1, 0.9, 0.9, 0.9))
		p.drawRect(xx(g.xmin - g.lsb), 0, xx(g.width) - xx(0.0), h)
		
		p.setColor(color(1, 0.7, 0.7, 0.7))
		p.drawRect(xx(g.xmin), yy(g.ymin), xx(g.xmax) - xx(g.xmin), yy(g.ymax) - yy(g.ymin))
		
		p.setColor(Black)
		p.drawStr(20, 20, index)
		
		p.drawStr(220, 20, g.code)
		string s = utf32_to_utf8(g.codes)
		#s.add(g.code)
		p.drawStr(300, 20, s)
		
		for c in g.contours
			p.setColor(Black)
			#print("--")
			for i in 0:c.p.num
				int i1 = (i+1) % c.p.num
				#print("" + c.p[i].x + "  " + c.p[i].y + "       ")
				p.drawLine(xx(c.p[i].x), yy(c.p[i].y), xx(c.p[i1].x), yy(c.p[i1].y))
				
			for i in 0:c.p.num
				int i1 = (i+1) % c.p.num
				
				if (c.flags[i] & 1) != 0
					p.setColor(Red)
				else
					p.setColor(Blue)
				p.drawCircle(xx(c.p[i].x), yy(c.p[i].y), 3)
		
		
TTF *font

void main(string[] arg)
	if arg.num == 0
		return
	
	font = new TTF
	if !font.load(arg[0])
		return
	
	if arg.num > 1
		font.export(arg[1])
	
	FontWindow *w = new FontWindow
	w.show()
	w.run()
	print("ok")


