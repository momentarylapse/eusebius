use os

class BinaryFile extends File
	func xread(p: void*, size: int) -> int
		cstring *pp = p
		string r = read(size)
		for i in 0:len(r)
			pp[i] = r[i]
		return len(r)

class Word
	c: char[2]
	func __int__() -> int
		int a = c[0]
		int b = c[1]
		return a + b * 256

class Inode
	rights: Word
	user: Word
	size: int
	atime, ctime, mtime, dtime: int
	group: Word
	hard_links: Word
	blocks: int
	flags: int
	version: int
	iblock: int[15]
	file_version: int
	acl: int
	size_high: int
	dummy: int
	xx_high: int[3]
	func isDir() -> bool
		return (int(rights) & 0xf000) == 0x4000
	func isFile() -> bool
		return (int(rights) & 0xf000) == 0x8000

class DirEntryHeader
	inode: int
	rec_len: Word
	name_len: char
	file_type: char

class SuperBlock
	inodes_count: int
	blocks_count: int
	reserved_blocks_count: int
	free_blocks_count: int
	free_inodes_count: int
	first_data_block: int
	log_block_size: int
	log_cluster_size: int
	blocks_per_group: int
	obso_frags_per_group: int
	inodes_per_group: int
	mtime, wtime: int
	mnt_count, max_mnt_count: Word
	magic: Word
	state: Word
	errors: Word
	minor_rev_level: Word
	lastcheck, checkinterval: int
	creator_os: int
	rev_level: int
	def_resuid, def_resgid: Word
	first_ino: int
	inode_size: Word
	block_group_nr: Word
	feature_compat, feature_incompat, feature_ro_compat: int
	uuid: int[16]
	volume_name: int[16]
	last_mounted: int[64]
	_filler_: int[1024-200]

class BlockGroupDescriptor
	block_bitmap: int
	inode_bitmap: int
	inode_table: int
	free_blocks_count, free_inodes_count: Word
	used_dirs_count: Word
	flags: Word
	exclude_bitmap: int
	block_bitmap_csum, inode_bitmap_csum: Word
	itable_unused: Word
	checksum: Word


class Ext2Reader
	f: BinaryFile*
	block_size: int
	block_offset: int
	inodes_per_block: int
	sb: SuperBlock
	bg: BlockGroupDescriptor
	readable, writable: bool
	
	filter: string[]
	show_details: bool
	
	func testCompatibility()
		readable = false
		writable = false
		if (sb.feature_incompat & 0xfffffffd) > 0
			print("---- not readable ----")
			print(sb.feature_incompat)
			return
		readable = true
		if (sb.feature_ro_compat & 0xfffffffe) > 0
			print("---- read only ----")
			print(sb.feature_ro_compat)
			return
		writable = true
	
	func read(filename: string)
		f = Filesystem.open(filename)
		if !f
			return
	
		# super block (always at offset 1024!)
		f.seek(1024)
		f.xread(&sb, sizeof(SuperBlock))
		block_size = 1 << (sb.log_block_size + 10)
		inodes_per_block = block_size / sizeof(Inode) #128
		block_offset = 1024
		if block_size == 1024
			block_offset = 0
		block_offset = 0 # ?????
		
		# block bmp (1k: #18 -> 0x4800)  (2k: #5 -> 0x2800)
		# inode bmp (1k: #19 -> 0x4c00)  (2k: #6 -> 0x3000)
	
		# block group descriptor (block #2)
		#f.set_pos(1024 + block_size)
		f.set_pos(1024 + 1024)
		f.xread(&bg, sizeof(BlockGroupDescriptor))
		
		if show_details
			showMetaData()
		testCompatibility()
		if readable
			if filter.num == 0
				print("Root:")
			showInodeRecursive(2, 0)
		
		del f
	
	func block_to_offset(index: int) -> int
		return block_offset + block_size * (index)
	
	func read_block(index: int) -> string
		f.set_pos(block_offset + block_size * index) ## (index - 1) ????
		return f.read(block_size)
	
	func read_inode(_index: int, inode: Inode)
		int index = _index + 1
		int group_no = (index - 1) / sb.inodes_per_group # 0...
		int offset = (index - 1) % sb.inodes_per_group
		# group_no!!!!!!!!!!!!!!
		int pos = block_offset
		pos += group_no * sb.blocks_per_group * block_size
		pos += bg.inode_table * block_size
		pos += offset * sizeof(Inode)
		#print("inode={{index}}  pos=0x{{pos|x}}")
		
		#print("0x{{block_to_offset(bg.inode_table) + (index - 1) * sizeof(Inode)|x}}")
		
		f.set_pos(pos)
		f.xread(&inode, sizeof(Inode))
	
	func readInodeContents(inode: Inode) -> string
		string r
		int num_blocks = (inode.size-1) / block_size + 1
		int size = num_blocks * block_size
		int num_direct = min(num_blocks, 12)
		for i in 0:num_direct
			r += read_block(inode.iblock[i])
		# indirect...
		
		r.resize(inode.size)
		return r
	
	func showInodeRecursive(index: int, level: int)
		Inode inode
		read_inode(index, inode)
		string indent
		for i in 0:level*2+1
			indent += "  "
		string info = "s={{inode.size|-5}}"
		#print inode.mtime
		if inode.isDir()
			info += "  dir"
		else if inode.isFile()
			info += "  file"
		else
			info += "  ???(0x{{int(inode.rights)|04x}})"
		info += "  i={{index}}"
		if show_details
			info += "  b[0]={{inode.iblock[0]}}"
		print(indent + info)
		
		if inode.isDir()
			string cont = readInodeContents(inode)
			int p = 0
			while p < cont.num
				DirEntryHeader *d = &cont[p]
				if int(d.rec_len) == 0
					break
				string name = cont[p + 8: p + 8 + int(d.name_len)]
				info = "  {{name|-12}}"
				#print "--"+name
				info += "  i={{d.inode|-4}}"
				int ft = d.file_type
				if ft == 2
					info += "  dir"
				else if ft == 1
					info += "  file"
				else
					info += "  ???({{ft}})"
				p += int(d.rec_len)
				if name.num == 0 or d.inode == 0
					continue
				if filter.num > 0
					if filter[level] != name
						continue
				print(indent + info)
				
				
				if not (name in [".", ".."])
				#	if ft == 2 or ft == 1
						showInodeRecursive(d.inode, level+1)
			
			#print(cont.substr(0, 32).hex(false))
	
	func showMetaData()
		print("block size: {{block_size}}")
		print("inodes per block: {{block_size/sizeof(Inode)}}")
		print("--------")
		print("inode count: {{sb.inodes_count}}")
		print("block count: {{sb.blocks_count}}")
		print("log block size: {{sb.log_block_size}}")
		print("first data block: {{sb.first_data_block}}")
		print("blocks per group: {{sb.blocks_per_group}}")
		print("inodes per group: {{sb.inodes_per_group}}")
		print("first ino: {{sb.first_ino}}")
		print("--------")
		print("block bmp: {{bg.block_bitmap}}")
		print("inode bmp: {{bg.inode_bitmap}}")
		print("inode table: {{bg.inode_table}}")

func main(arg: string[])
	string filename
	Ext2Reader r
	r.show_details = false
	for i in 0:arg.num
		if arg[i] == "-v"
			r.show_details = true
		else if arg[i] == "--filter"
			i ++
			r.filter = arg[i].explode("/")
		else
			filename = arg[i]
	r.read(filename)
